[import env
  [mem [memory [i32] 1]]
  [log
    [func
      [par [pointer [exp mem] [array [u8]]]
       n-bytes [i32]]
      [tuple]]]
  [log-i32
    [func
      [n [i32]]
      [tuple]]]
  [log-i64
    [func
      [n [i64]]
      [tuple]]]
      ]

[comptimefn log-ar [sz [exp [i32]]] [byte-array [array [u8] sz]] [tuple]
  [log [data mem byte-array] [reify sz]]]

[comptimefn log-abort [sz [exp [i32]]] [byte-array [array [u8] sz]] [union]
  [log [data mem byte-array] [reify sz]]
  [intrinsic unreachable]]

[def 0 [i32 0]]
[def 1 [i32 1]]
[def 2 [i32 2]]
[def 3 [i32 3]]
[def 4 [i32 4]]
[def 8 [i32 8]]
[def 16 [i32 16]]

[def false [i32 0]]
[def true [i32 1]]

[defn not [x [i32]] [i32] [if x 0 1]]

[defn eq [x [i32] y [i32]] [i32] [intrinsic i32.eq x y]]
[defn ne [x [i32] y [i32]] [i32] [intrinsic i32.ne x y]]
[defn lt-s [x [i32] y [i32]] [i32] [intrinsic i32.lt-s x y]]
[defn lt-u [x [i32] y [i32]] [i32] [intrinsic i32.lt-u x y]]
[defn le-s [x [i32] y [i32]] [i32] [intrinsic i32.le-s x y]]
[defn le-u [x [i32] y [i32]] [i32] [intrinsic i32.le-u x y]]
[defn is-negative [x [i32]] [i32] [lt-s x 0]]

[defn min [x [i32] y [i32]] [i32] [if [lt-s x y] x y]]
[defn max [x [i32] y [i32]] [i32] [if [lt-s x y] y x]]

[defn add [x [i32] y [i32]] [i32] [intrinsic i32.add x y]]
[defn sub [x [i32] y [i32]] [i32] [intrinsic i32.sub x y]]
[defn mul [x [i32] y [i32]] [i32] [intrinsic i32.mul x y]]

[defn inc [x [i32]] [i32] [add x 1]]
[defn dec [x [i32]] [i32] [sub x 1]]

[defn bitwise-and [x [i32] y [i32]] [i32] [intrinsic i32.and x y]]
[defn bitwise-ior [x [i32] y [i32]] [i32] [intrinsic i32.or x y]]
[defn bitwise-xor [x [i32] y [i32]] [i32] [intrinsic i32.xor x y]]

[defn is-odd [x [i32]] [i32] [bitwise-and x 1]]

[datatype pmem [a datatype]
  [pointer [exp mem] a]]

[def static-mem-top [memory-static-top mem]]

[defn static-is [p [pmem -a]] [i32]
  [lt-s [cast [i32] p] static-mem-top]]

[mutable mem-top [memory-static-top mem]]

[defn get-top [] [i32]
  mem-top]

[defn set-top [new-top [i32]] [tuple]
  [assign mem-top new-top]]

[defn align [p [i32] x [i32]] [i32]
  [bitwise-and
    [add x [dec p]]
    [bitwise-xor [i32 -1] [dec p]]]]

[defn mem-align [p [i32]] [i32]
  [align [i32 16] p]]

[defn mem-align-is [p [i32]] [i32]
  [eq p [mem-align p]]]

[defn memory-fill [p [pmem -a] value [i32] n-bytes [i32]] [tuple]
  [intrinsic memory.fill mem [cast [i32] p] value n-bytes]]

[defn memory-zero [p [pmem -a] n-bytes [i32]] [tuple]
  [memory-fill p 0 n-bytes]]

[defn memory-copy [pdst [pmem -a] psrc [pmem -a] n-bytes [i32]] [tuple]
  [intrinsic memory.copy mem [cast [i32] pdst] [cast [i32] psrc] n-bytes]]

[defn memory-compare [p1 [pmem [array [u8]]] p2 [pmem [array [u8]]] n-bytes [i32]] [i64]
  [loop [i 0]
    [if [lt-s i n-bytes]
      [let [diff [intrinsic i64.sub [deref [pindex p1 i]] [deref [pindex p2 i]]]]
        [if [intrinsic i64.eq diff [i64 0]]
          [continue i [inc i]]
          diff]]
      [i64 0]]]]

[defn memory-size-bytes [] [i32]
  [intrinsic i32.shl [intrinsic memory.size mem] [i32 16]]]

[def null [cast [pmem -a] [i32 0]]]

[defn is-null [p [pmem -a]] [i32]
  [eq [cast [i32] p] 0]]

[defn resize-mem-to-fit [new-top [i32]] [tuple]
  [let
    [mem-size [memory-size-bytes]]
    [if [lt-s mem-size new-top]
      [let
        [missing-bytes [sub new-top mem-size]
         missing-pages [intrinsic i32.shr-s [align [i32 65536] missing-bytes] [i32 16]]
         res [intrinsic memory.grow mem missing-pages]]
        [when [is-negative res]
          [log-i32 missing-bytes]
          [log-i32 missing-pages]
          [log-abort [text 'resize-mem-to-fit memory.grow failed']]]
        [log-ar [text 'resize-mem-to-fit grew mem pages']]
        [log-i32 missing-bytes]
        [log-i32 missing-pages]]]]]

[defn resize-mem [frame-size [i32]] [tuple]
  [let
    [new-top [add [get-top] frame-size]]
    [resize-mem-to-fit new-top]
    [set-top new-top]]]

[datatype alloc-stack-frame [byte-aligned-size [exp [i32]]]
  [record
    [frame-size [i32]]
    [byte-size [i32]]
    [byte-aligned-size [i32]]
    [realloc-size [i32]] ; a size less than equal to the original for when the block is reallocated to a smaller size in place
    [abandoned [i32]] ; when the block could not be reallocated in place
    [data [array [u8] byte-aligned-size]]
    [canary [i64]]
    [frame-size-end [i32]]
    ]]

; cacacadeadbeef in little endian
[def canary-value [i64 0xefbeaddecacaca]]

[mutable num-of-frames [i32 0]]

[defn get-number-of-frames [] [i32] num-of-frames]

[export get-number-of-frames]

[defn alloc-frame-log [pframe [pmem [alloc-stack-frame -a]]] [tuple]
  [let
    [frame-size [deref [pfield pframe frame-size]]
     byte-size [deref [pfield pframe byte-size]]
     realloc-size [deref [pfield pframe realloc-size]]
     byte-aligned-size [deref [pfield pframe byte-aligned-size]]
     abandoned [deref [pfield pframe abandoned]]
     pframe-sized [cast [pmem [alloc-stack-frame [exp byte-aligned-size]]] pframe]
     canary [deref [pfield pframe-sized canary]]
     frame-size-end [deref [pfield pframe-sized frame-size-end]]]
  [log-ar [text 'alloc-frame-log']]
  [log-i32 frame-size]
  [log-i32 byte-size]
  [log-i32 realloc-size]
  [log-i32 byte-aligned-size]
  [log-i32 abandoned]
  [log-ar [text 'alloc-frame-log data start']]
  [log [pfield pframe data] byte-size]
  [log-ar [text 'alloc-frame-log data end']]
  ]]

[defn alloc-frame-validate [pframe [pmem [alloc-stack-frame -a]]] [tuple]
  ; todo check if top is within memory bounds
  [if [static-is pframe]
    [log-abort [text 'alloc-frame-validate pframe points to static mem']]]
  [if [lt-s [memory-size-bytes] [add [cast [i32] pframe] [size-of-type [alloc-stack-frame [exp [mem-align 1]]]]]]
    [log-abort [text 'alloc-frame-validate memory-size-bytes < frame-size']]]
  [let
    [frame-size [deref [pfield pframe frame-size]]
     byte-size [deref [pfield pframe byte-size]]
     realloc-size [deref [pfield pframe realloc-size]]
     byte-aligned-size [deref [pfield pframe byte-aligned-size]]
     abandoned [deref [pfield pframe abandoned]]
     pframe-sized [cast [pmem [alloc-stack-frame [exp byte-aligned-size]]] pframe]
     canary [deref [pfield pframe-sized canary]]
     frame-size-end [deref [pfield pframe-sized frame-size-end]]]
    [if [lt-s frame-size [size-of-type [alloc-stack-frame [exp 0]]]]
      [log-abort [text 'validate-alloc frame-size < size-of-type alloc-stack-frame 0']]]
    [if [ne [mem-align byte-size] byte-aligned-size]
      [log-abort [text 'validate-alloc byte-size != byte-aligned-size']]]
    [if [lt-s byte-size realloc-size]
      [log-abort [text 'validate-alloc byte-size < realloc-size']]]
    [if [lt-s abandoned 0] [log-abort [text 'validate-alloc abandoned < 0']]]
    [if [lt-s 1 abandoned] [log-abort [text 'validate-alloc abandoned > 1']]]
    ; todo check bytes after realloc-size and before byte-size are overwritten
    ; todo check bytes after byte-size and before byte-aligned-size are properly zeroed
    [if [intrinsic i64.ne canary canary-value] [log-abort [text 'validate-alloc canary value mismatch']]]
    [if [ne frame-size frame-size-end] [log-abort [text 'validate-alloc frame-size != frame-size-end']]]]]

[defn alloc-frames-validate-all [] [i32]
  [loop
    [cur-top [get-top]
     i static-mem-top
     n-frames 0]
    [if [lt-s i cur-top]
      [let
        [pframe [cast [pmem [alloc-stack-frame -a]] i]
         frame-size [deref [pfield pframe frame-size]]]
        [alloc-frame-validate pframe]
        [continue i [add i frame-size] n-frames [inc n-frames]]]
      [do
        [if [ne n-frames num-of-frames]
          [log-abort [text 'alloc-frames-validate-all n-frames != num-of-frames']]]
        n-frames]]]]

[export alloc-frames-validate-all]

[defn alloc-stack-frame-push [byte-size [i32]] [pmem -a]
  [let
    [byte-aligned-size [mem-align byte-size]
     frame-size [size-of-type [alloc-stack-frame [exp byte-aligned-size]]]
     cur-top [get-top]
     pframe [cast [pmem [alloc-stack-frame [exp byte-aligned-size]]] cur-top]
     frame-data [pfield pframe data]]
    [resize-mem frame-size]
    [passign pframe
      [record
        frame-size frame-size
        byte-size byte-size
        byte-aligned-size byte-aligned-size
        realloc-size byte-size
        abandoned 0]]
    [memory-zero frame-data byte-aligned-size]
    [passign [pfield pframe canary] canary-value]
    [passign [pfield pframe frame-size-end] frame-size]
    [alloc-frame-validate pframe]
    [assign num-of-frames [inc num-of-frames]]

    frame-data]]

[defn alloc-stack-frame-update [pframe [pmem [alloc-stack-frame -a]] new-byte-size [i32]] [pmem -a]
  [if [eq new-byte-size 0] [log-abort [text 'alloc-stack-frame-update new-byte-size == 0']]]
  [let
    [old-frame-size [deref [pfield pframe frame-size]]
     byte-aligned-size [mem-align new-byte-size]
     pframe-sized [cast [pmem [alloc-stack-frame [exp byte-aligned-size]]] pframe]
     frame-size [size-of-type [alloc-stack-frame [exp byte-aligned-size]]]
     frame-data [pfield pframe data]]
    [resize-mem [sub frame-size old-frame-size]]
    [passign pframe
      [record
        frame-size frame-size
        byte-size new-byte-size
        byte-aligned-size byte-aligned-size
        realloc-size new-byte-size
        abandoned 0]]
    [passign [pfield pframe-sized canary] canary-value]
    [passign [pfield pframe-sized frame-size-end] frame-size]

    frame-data]]

[mutable memory-overwrite-value [i32 0xa0]]

[defn memory-overwrite [pdata [pmem -a] n-bytes [i32]] [tuple]
  [if [lt-s n-bytes 0] [log-abort [text 'overwrite n-bytes < 0']]]
  ; write between 0xa0 and 0xfe to the memory
  [let [overwrite-val memory-overwrite-value]
    [memory-fill pdata overwrite-val n-bytes]
    [assign memory-overwrite-value
      [if [eq overwrite-val [i32 0xfe]]
        [i32 0xa0]
        [inc overwrite-val]]]]]

[defn alloc-stack-frame-abandon [pframe [pmem [alloc-stack-frame -a]]] [tuple]
  ; overwrite data area to avoid dangling pointers using it
  [memory-overwrite [pfield pframe data] [deref [pfield pframe byte-aligned-size]]]
  [passign [pfield pframe abandoned] 1]]

[defn get-top-frame [] [pmem [alloc-stack-frame -a]]
  [let [cur-top [get-top]]
    [if [eq cur-top static-mem-top]
      [cast [pmem [alloc-stack-frame -a]] null]
      [let
        [pframe-size [cast [pmem [i32]] [sub cur-top [size-of-type [i32]]]]
         frame-size [deref pframe-size]
         pframe [cast [pmem [alloc-stack-frame -a]] [sub cur-top frame-size]]]
        [alloc-frame-validate pframe]
        pframe]]]]

[defn alloc-frame-top-log [] [tuple]
  [let [top-frame [get-top-frame]]
    [if [is-null top-frame]
      [log-ar [text 'alloc-frame-top-log stack is empty']]
      [alloc-frame-log top-frame]]]]

[export alloc-frame-top-log]

[defn alloc-stack-pop [] [tuple]
  [let [pframe [get-top-frame]]
    [if [is-null pframe]
      [log-abort [text 'alloc-stack-pop stack empty']]]
    [set-top [cast [i32] pframe]]
    [assign num-of-frames [dec num-of-frames]]
    [memory-zero pframe [deref [pfield pframe frame-size]]]]]

; inspired by the lua lua_Alloc function signature
; https://ligurio.github.io/lua-c-manual-pages/lua_Alloc.3.html
; from https://nullprogram.com/blog/2023/12/17/

[defn realloc-debug [old-p [pmem -a] old-n-bytes [i32] new-n-bytes [i32]] [pmem -a]
  [ifs
    [is-null old-p]
    [do
      [if [ne old-n-bytes 0] [log-abort [text 'realloc old-p == 0 && old-n-bytes != 0']]]
      [alloc-stack-frame-push new-n-bytes]]

    [eq old-n-bytes 0]
    [do
      [if [ne [cast [i32] old-p] 0] [log-abort [text 'realloc old-n-bytes == 0 && old-p != 0']]]
      [alloc-stack-frame-push new-n-bytes]]

    [let
      ; todo check if old-p is a valid pointer first, non static and within memory bounds
      ; find the frame from the data pointer
      [pframe-i [sub [cast [i32] old-p] [offset-of [alloc-stack-frame -a] data]]
       pframe [cast [pmem [alloc-stack-frame -a]] pframe-i]
       frame-end [add pframe-i [deref [pfield pframe frame-size]]]]
      [alloc-frame-validate pframe]
      [if [eq [deref [pfield pframe abandoned]] 1]
        [log-abort [text 'realloc frame is abandoned, should not happen']]]

      ; check if old-n matches the frame realloc-size
      [if [ne old-n-bytes [deref [pfield pframe realloc-size]]]
        [log-abort [text 'realloc old-n-bytes != realloc-size']]]
      ; if the frame is the top frame
      [if [eq [get-top] frame-end]
        ; if we are freeing the frame
        [if [eq new-n-bytes 0]
          [do
            ; pop top frame
            [alloc-stack-pop]
            ; pop abandoned frames till we find a non-abandoned frame or the bottom of the stack
            [loop []
              [let [ptframe [get-top-frame]]
                [if [not [is-null ptframe]]
                  [if [eq [deref [pfield ptframe abandoned]] 1]
                    [do
                      [alloc-stack-pop]
                      [continue]]]]]]
            null]
          [do
            [if [eq [deref [pfield pframe abandoned]] 1]
              [log-abort [text 'realloc top frame is abandoned, should not happen']]]
            ; if we are updating the size of the frame
            [if [ne old-n-bytes new-n-bytes]
              [alloc-stack-frame-update pframe new-n-bytes]
              old-p]]]
        ; else, frame is not the last frame
        [ifs
          [eq new-n-bytes 0]
          [do
            [alloc-stack-frame-abandon pframe]
            null]

          [eq old-n-bytes new-n-bytes]
          old-p

          ; if we are within the frame size
          [le-s new-n-bytes [deref [pfield pframe byte-size]]]
          [do
            [if [eq [deref [pfield pframe abandoned]] 1]
              [log-abort [text 'realloc cannot update abandoned frame']]]
            [passign [pfield pframe realloc-size] new-n-bytes]
            [if [lt-s old-n-bytes new-n-bytes]
              ; zero new memory area
              [memory-zero
                [cast [pmem -a] [add [pfield pframe data] old-n-bytes]]
                [sub new-n-bytes old-n-bytes]]
              ; overwrite now unused parts of the data area to avoid dangling pointers using it
              [memory-overwrite
                [cast [pmem -a] [add [pfield pframe data] new-n-bytes]]
                [sub old-n-bytes new-n-bytes]]]
            old-p]
          ; else, allocate a new frame copying over old data, then abandon the old frame
          [let
            [new-data [alloc-stack-frame-push new-n-bytes]]
            [memory-copy new-data old-p old-n-bytes]
            [alloc-stack-frame-abandon pframe]
            new-data]]]]]]

[export realloc-debug]

[defn pointer-dynamic-is [p [pmem -a]] [i32]
  [if [is-null p]
    1
    [let [pi [cast [i32] p]]
      [if [lt-u pi static-mem-top]
      0
      [lt-u pi [memory-size-bytes]]]]]]

[defn realloc-production [old-p [pmem -a] old-n-bytes [i32] new-n-bytes [i32]] [pmem -a]
  [if [not [mem-align-is [cast [i32] old-p]]] [log-abort [text 'realloc-production old-p not aligned']]]
  [if [not [pointer-dynamic-is old-p]] [log-abort [text 'realloc-production old-p not dynamic']]]
  [let
    [old-n-aligned [mem-align old-n-bytes]
     new-n-aligned [mem-align new-n-bytes]
     old-pi [cast [i32] old-p]
     old-end [add old-pi old-n-aligned]
     cur-top [get-top]]
    [if [eq old-end cur-top]
      ; reallocating the top frame
      [do
        ; ensure the memory fits the new size
        [if [lt-u old-n-aligned new-n-aligned]
          [do
            [resize-mem [sub new-n-aligned old-n-aligned]]
            [memory-zero [cast [pmem -a] [add old-pi old-n-bytes]] [sub new-n-bytes old-n-bytes]]
            [memory-overwrite [cast [pmem -a] [add old-pi new-n-bytes]] [sub new-n-aligned new-n-bytes]]]]
        old-p]
      [if [le-u new-n-bytes old-n-bytes]
        ; not the top frame but smaller than before
        [do
          ; overwrite now unused parts of the data area to avoid dangling pointers using it
          [memory-overwrite [cast [pmem -a] [add old-pi new-n-bytes]] [sub old-n-bytes new-n-bytes]]
          old-p]
        ; else allocating a new frame
        [let [new-p [cast [pmem -a] cur-top]]
          [resize-mem new-n-aligned]
          ; copy old data to new location
          [memory-copy new-p old-p old-n-bytes]
          ; abandon old frame
          [memory-overwrite old-p old-n-aligned]
          new-p]]]]]

[def is-debug [i32 1]]

[defn realloc [old-p [pmem -a] old-n-bytes [i32] new-n-bytes [i32]] [pmem -a]
  [if is-debug
    [realloc-debug old-p old-n-bytes new-n-bytes]
    [realloc-production old-p old-n-bytes new-n-bytes]]]

[defn alloc-bytes [n-bytes [i32]] [pmem -a]
  [realloc null 0 n-bytes]]

[defn free [old-p [pmem -a] old-n-bytes [i32]] [tuple]
  [realloc old-p old-n-bytes 0]
  [do]]

[genfn alloc-fn [t datatype] [] [pmem t]
  [alloc-bytes [size-of-type t]]]

[genfn alloc-init [t datatype] [v t] [pmem t]
  [let [p [[instantiate alloc-fn t]]]
    [pinit p v]
    p]]

[defn is-between-inclusive [lower [i64] c [i64] upper [i64]] [i32]
  [bitwise-and [intrinsic i64.le-s lower c] [intrinsic i64.le-s c upper]]]

[defn is-whitespace [c [i64]] [i32]
  [bitwise-ior [intrinsic i64.eq c [i64 32]] [intrinsic i64.eq c [i64 10]]]]

[defn is-word-char [c [i64]] [i32]
  [bitwise-ior
    [is-between-inclusive [i64 97] c [i64 122]]
    [is-between-inclusive [i64 45] c [i64 57]]]]

[datatype list-data [a datatype size [exp [i32]]]
  [record
    [size [i32]]
    [array [array a size]]]]

[datatype plist [vp datatype]
  [pmem [list-data vp -size]]]

[comptimefn static-list [t datatype sz [exp [i32]]] [array [array t sz]] [plist t]
  [data mem
    [record
      size [reify sz]
      array array]]]

[def empty-list [cast [plist -a] [static-list [array [i32]]]]]

[defn list-size [pvec [pmem [record [size [i32]]]]] [i32]
  [deref [pfield pvec size]]]

[export list-size]

[defn list-empty-is [pvec [pmem [record [size [i32]]]]] [i32]
  [eq [i32 0] [list-size pvec]]]

[genfn list-alloc [elem-type datatype] [lafn-sz [i32]] [plist elem-type]
  ; return empty list if size is 0
  [if [eq lafn-sz 0]
    empty-list
    [let [p [[instantiate alloc-fn [list-data elem-type [exp lafn-sz]]]]]
      [passign [pfield p size] lafn-sz]
      p]]]

[genfn list-free [elem-type datatype] [pvec [pmem [list-data elem-type -sz]]] [tuple]
  [if [not [static-is pvec]]
    [free pvec [size-of-type [list-data elem-type [exp [deref [pfield pvec size]]]]]]]]

[defn list-alloc-byte [sz [i32]] [plist [u8]] [[instantiate list-alloc [u8]] sz]]
[export list-alloc-byte]

[genfn list-get-p [elem-type datatype] [pvec [pmem [list-data elem-type -sz]] i [i32]] [pmem elem-type]
  [if [lt-s i 0] [log-abort [text 'list-get-p index negative']]]
  [if [le-s [list-size pvec] i] [log-abort [text 'list-get-p index out of bounds']]]
  [pindex [pfield pvec array] i]]

[genfn list-get [elem-type datatype] [pvec [pmem [list-data elem-type -sz]] i [i32]] elem-type
  [if [lt-s i 0] [log-abort [text 'list-get index negative']]]
  [if [le-s [list-size pvec] i] [log-abort [text 'list-get index out of bounds']]]
  [deref [pindex [pfield pvec array] i]]]

[genfn list-last [elem-type datatype] [pvec [pmem [list-data elem-type -sz]]] elem-type
  [if [list-empty-is pvec] [log-abort [text 'list-last list is empty']]]
  [deref [pindex [pfield pvec array] [dec [list-size pvec]]]]]

[genfn list-set [elem-type datatype] [pvec [pmem [list-data elem-type -sz]] i [i32] elem elem-type] [tuple]
  [if [lt-s i 0] [log-abort [text 'list-set index negative']]]
  [if [le-s [list-size pvec] i] [log-abort [text 'list-set index out of bounds']]]
  [passign [pindex [pfield pvec array] i] elem]]

[genfn list-data-array [a datatype] [pvec [plist a]] [pmem [array a]]
  [pfield pvec array]]

[defn list-data-byte-array [pvec [plist [u8]]] [pmem [array [u8]]]
  [list-data-array pvec]]

[export list-data-byte-array]

[datatype growable-list [a datatype]
  [record
    [size [i32]]
    [capacity [i32]]
    [parray [pmem [array a]]]]]

[datatype p-growable-list [a datatype]
  [pmem [growable-list a]]]

[genfn growable-list-alloc-init [elem-type datatype] [init-capacity [i32]] [p-growable-list elem-type]
  [when [le-s init-capacity 0]
    [log-abort [text 'growable-list-alloc-init init-capacity <= 0']]]
  [alloc-init
    [record
      size 0
      capacity init-capacity
      parray [[instantiate alloc-fn [array elem-type [exp init-capacity]]]]]]]

[genfn growable-list-free [elem-type datatype] [pglist [p-growable-list elem-type]] [tuple]
  [let
    [parray [deref [pfield pglist parray]]]
    [free parray [size-of-type [array elem-type [exp [deref [pfield pglist capacity]]]]]]
    [free pglist [size-of-type [growable-list elem-type]]]]]

[genfn growable-list-resize [elem-type datatype] [pglist [p-growable-list elem-type] new-capacity [i32]] [tuple]
  [let
    [cur-capacity [deref [pfield pglist capacity]]
     cur-size [deref [pfield pglist size]]]
    [when [le-s new-capacity cur-size]
      [log-abort [text 'growable-list-resize new-capacity < cur-size']]]
    [passign [pfield pglist capacity] new-capacity]
    [passign [pfield pglist parray]
      [cast [pmem [array elem-type]]
        [realloc
          [deref [pfield pglist parray]]
          [size-of-type [array elem-type [exp cur-capacity]]]
          [size-of-type [array elem-type [exp new-capacity]]]]]]]]

[genfn growable-list-push [elem-type datatype] [pglist [p-growable-list elem-type] elem elem-type] [tuple]
  [let
    [cur-size [deref [pfield pglist size]]
     cur-capacity [deref [pfield pglist capacity]]]
    [when [le-s cur-capacity 0]
      [log-abort [text 'growable-list-push capacity <= 0']]]
    [if [eq cur-size cur-capacity]
      [growable-list-resize pglist [mul cur-capacity 2]]]
    [passign [pindex [deref [pfield pglist parray]] cur-size] elem]
    [passign [pfield pglist size] [inc cur-size]]]]

[genfn growable-list-get-p [elem-type datatype] [pglist [p-growable-list elem-type] i [i32]] [pmem elem-type]
  [if [lt-s i 0] [log-abort [text 'growable-list-get i negative']]]
  [if [le-s [list-size pglist] i] [log-abort [text 'growable-list-get i out of bounds']]]
  [pindex [deref [pfield pglist parray]] i]]

[genfn growable-list-get [elem-type datatype] [pglist [p-growable-list elem-type] i [i32]] elem-type
  [if [lt-s i 0] [log-abort [text 'growable-list-get i negative']]]
  [if [le-s [list-size pglist] i] [log-abort [text 'growable-list-get i out of bounds']]]
  [deref [pindex [deref [pfield pglist parray]] i]]]

[genfn growable-list-to-list [elem-type datatype] [pglist [p-growable-list elem-type]] [plist elem-type]
  [let
    [cur-size [deref [pfield pglist size]]
     plist [[instantiate list-alloc elem-type] cur-size]]
    [loop [i 0]
      [when [lt-s i cur-size]
        [passign
          [pindex [pfield plist array] i]
          [deref [pindex [deref [pfield pglist parray]] i]]]
        [continue i [inc i]]]]
    plist]]

[defn growable-list-reset [pglist [p-growable-list -a]] [tuple]
  [passign [pfield pglist size] 0]]

[datatype text []
  [plist [u8]]]

[defn log-text [txt [text]] [tuple]
  [log [list-data-array txt] [list-size txt]]]

[datatype p-assoc-list [key datatype value datatype]
  [p-growable-list [tuple key value]]]

[genfn assoc-list-push [k datatype v datatype] [pglist [p-assoc-list k v] key k value v] [tuple]
  [growable-list-push pglist [tuple key value]]]

[genfn assoc-list-get-value [k datatype v datatype] [pglist [p-assoc-list k v] key k] [pmem v]
  [loop
    [cur-size [deref [pfield pglist size]]
     i 0]
    [if [lt-s i cur-size]
      [if [eq [deref [pindex [pindex [deref [pfield pglist parray]] i] [i32 0]]] key]
        [pindex [pindex [deref [pfield pglist parray]] i] [i32 1]]
        [continue i [inc i]]]
      [cast [pmem [tuple k v]] [i32 0]]]]]

[def form-word-tag [i32 10]]
[def form-list-tag [i32 11]]

[datatype form-word []
  [record
    [tag [exp form-word-tag]]
    [pword [text]]]]

[datatype
  form-list []
  [record
    [tag [exp form-list-tag]]
    [forms [plist [pmem [form]]]]]

  form []
  [union
    [form-word]
    [form-list]]]

[datatype pform []
  [pmem [form]]]

[defn form-word-alloc [ptext [text]] [pform]
  [alloc-init
    [record
      tag [annotation [exp form-word-tag] form-word-tag]
      pword ptext]]]

[defn form-list-alloc [pfs [plist [pform]]] [pform]
  [alloc-init
    [record
      tag [annotation [exp form-list-tag] form-list-tag]
      forms pfs]]]

[defn form-tag [pf [pform]] [i32]
  [deref [pfield pf tag]]]

[export form-tag]

[defn form-word-is [pf [pform]] [i32]
  [eq [deref [pfield pf tag]] form-word-tag]]

[export form-word-is]

[defn form-word-get [pf [pform]] [text]
  [if [not [form-word-is pf]] [log-abort [text 'form-word-get not a form-word']]]
  [let [pfw [cast [pmem [form-word]] pf]]
    [deref [pfield pfw pword]]]]

[export form-word-get]

[defn form-list-is [pf [pform]] [i32]
  [eq [deref [pfield pf tag]] form-list-tag]]

[export form-list-is]

[defn form-list-get [pf [pform]] [plist [pform]]
  [if [not [form-list-is pf]] [log-abort [text 'form-list-get not a form-list']]]
  [let [pfl [cast [pmem [form-list]] pf]]
    [deref [pfield pfl forms]]]]

[export form-list-get]

[defn list-get-form [pfl [plist [pform]] i [i32]] [pform]
  [list-get pfl i]]

[export list-get-form]

[defn text-free [txt [text]] [tuple]
  [list-free txt]]

[export text-free]

[defn form-free [pf [pform]] [tuple]
  [if [not [static-is pf]]
    [ifs
      [form-word-is pf]
      ; we no longer free the word here as they can be shared between forms
      [free pf [size-of-type [form-word]]]

      [form-list-is pf]
      [let [forms [form-list-get pf]]
        [loop [i 0 n [list-size forms]]
          [if [lt-s i n]
            [do
              [form-free [list-get forms i]]
              [continue i [inc i]]]]]
        [list-free forms]
        [free pf [size-of-type [form-list]]]]

      [log-abort [text 'form-free unknown form type']]]]]

[defn list-forms-free [pfs [plist [pform]]] [tuple]
  [loop [i 0 n [list-size pfs]]
    [if [lt-s i n]
      [do
        [form-free [list-get pfs i]]
        [continue i [inc i]]]]
  [list-free pfs]]]

[export form-free list-forms-free]

[defn scan-word-end [bytes [plist [u8]] start [i32]] [i32]
  [loop [i start n-of-bytes [list-size bytes]]
    [if [lt-s i n-of-bytes]
      [if [is-word-char [list-get bytes i]]
        [continue i [inc i]]
        i]
      i]]]

[defn make-stack [stack-size [i32] init-capacity [i32]] [pmem [array [p-growable-list [pform]]]]
  [if [le-s init-capacity 0] [log-abort [text 'make-stack init-capacity <= 0']]]
  [let [parray [[instantiate alloc-fn [array [p-growable-list [pform]] [exp stack-size]]]]]
    [loop [i 0]
      [if [lt-s i stack-size]
        [do
          [passign
            [pindex parray i]
            [[instantiate growable-list-alloc-init [pform]] init-capacity]
            ]
          [continue i [inc i]]]]]
    parray]]

[defn stack-free [pstack [pmem [array [p-growable-list [pform]]]] stack-size [i32]] [tuple]
  [loop [i 0]
    [if [lt-s i stack-size]
      [do
        [growable-list-free [deref [pindex pstack i]]]
        [continue i [inc i]]]]]
  [free pstack [size-of-type [array [p-growable-list [pform]] [exp stack-size]]]]]

[def stack-size [i32 8]]
[def init-capacity [i32 8]]

[defn list-slice-byte-array [psrc [pmem [array [u8]]] sz [i32]] [plist [u8]]
  [let
    [pdst [list-alloc-byte sz]]
    [memory-copy [list-data-array pdst] psrc sz]
    pdst]]

[defn unwind-stack [stack [pmem [array [p-growable-list [pform]]]] stack-index [i32]] [pform]
  [if [lt-s stack-index 0] [log-abort [text 'unwind-stack stack index negative']]]
  [loop [i stack-index]
    [let [glist [deref [pindex stack i]]
          form-list [growable-list-to-list glist]]
      [growable-list-reset glist]
      [let [fl [form-list-alloc form-list]]
        [if [eq i 0]
          fl
          [do
            [growable-list-push [deref [pindex stack [dec i]]] fl]
            [continue i [dec i]]]]]]]]

[defn text-intern [texts [p-growable-list [text]] pw [pmem [array [u8]]] word-size [i32]] [text]
  [loop [txts-i 0 txts-sz [list-size texts]]
    [if [lt-s txts-i txts-sz]
      [let
        [cur-txt [growable-list-get texts txts-i]
         cur-sz [list-size cur-txt]]
        [if [eq cur-sz word-size]
          [if [intrinsic i64.eq [i64 0] [memory-compare [list-data-array cur-txt] pw word-size]]
            cur-txt
            [continue txts-i [inc txts-i]]]
          [continue txts-i [inc txts-i]]]]
      [cast [text] [i32 0]]]]]

[defn parse-n [bytes [text]]
  [tuple
    [p-growable-list [pform]]
    [p-growable-list [text]]
    [p-assoc-list [pform] [i32]]
    ]
  [let
    [stack [make-stack stack-size init-capacity]
     n-of-bytes [list-size bytes]
     bytes-array [list-data-array bytes]
     result [[instantiate growable-list-alloc-init [pform]] [i32 16]]
     texts [[instantiate growable-list-alloc-init [text]] [i32 10]]
     form-offsets [[instantiate growable-list-alloc-init [tuple [pform] [i32]]] [i32 128]]]
    [loop
      [i 0
       ; todo let stack-index be 0 based instead and result be the top growable-list
       stack-index [i32 -1]]
      [if [not [lt-s i n-of-bytes]]
        [if [not [is-negative stack-index]]
          [growable-list-push result [unwind-stack stack stack-index]]]
        [let [c [list-get bytes i]]
          [ifs
            [is-word-char c]
            [let
              [word-end [scan-word-end bytes [inc i]]
               word-size [sub word-end i]
               p [cast [pmem [array [u8]]] [pindex bytes-array i]]
               w-opt [text-intern texts p word-size]
               w
               [if [eq w-opt [i32 0]]
                [let
                  [new-word [list-slice-byte-array p word-size]]
                  [growable-list-push texts new-word]
                  new-word]
                  w-opt]
               fw [form-word-alloc w]]
              [assoc-list-push form-offsets fw i]
              [growable-list-push
                [if [is-negative stack-index]
                  result
                  [deref [pindex stack stack-index]]]
                fw]
              [continue i word-end]]

            [is-whitespace c]
            [continue i [inc i]]

            [intrinsic i64.eq c [i64 91]]
            [do
              [if [lt-s stack-size stack-index] [log-abort [text 'stack max depth reached']]]
              [continue
                i [inc i]
                stack-index [inc stack-index]]]

            [intrinsic i64.eq c [i64 93]]
            [do
              [if [is-negative stack-index] [log-abort [text 'unmatched end bracket']]]
              [let [glist [deref [pindex stack stack-index]]
                    form-list [growable-list-to-list glist]]
                [growable-list-reset glist]
                [let [fl [form-list-alloc form-list]]
                  [assoc-list-push form-offsets fl i]
                  [if [eq stack-index 0]
                    [growable-list-push result fl]
                    [growable-list-push [deref [pindex stack [dec stack-index]]] fl]]
                  [continue
                    i [inc i]
                    stack-index [dec stack-index]]]]]

            [log-abort [text 'unexpected character']]]]]]
        [stack-free stack stack-size]
        [tuple result texts
        form-offsets
        ]]]

[defn parse-all-forms [bytes [text]] [plist [pform]]
  [let
    [[gforms gtexts gform-offsets] [parse-n bytes]
     forms [growable-list-to-list gforms]]
    [growable-list-free gtexts]
    [growable-list-free gforms]
    [growable-list-free gform-offsets]
    forms]]

[export parse-all-forms]

[defn word-eq [w1 [text] w2 [text]] [i32]
  [if [eq w1 w2]
    true
    [let [n1 [list-size w1]
          n2 [list-size w2]]
      [if [eq n1 n2]
        [intrinsic i64.eq [i64 0] [memory-compare [list-data-array w1] [list-data-array w2] n1]]
        false]]]]

[def exp-integer-tag [i32 1]]
[def exp-var-tag [i32 2]]
[def exp-if-tag [i32 3]]
[def exp-intrinsic-tag [i32 4]]

[datatype boolean [] [i32]]

[def type-error-tag [i32 3]]
[def type-integer-tag [i32 1]]
[def type-tuple-tag [i32 2]]

[datatype type-error []
  [record
    [tag [exp type-error-tag]]]]

[datatype type-integer []
  [record
    [tag [exp type-integer-tag]]
    [n-bytes [i32]]
    [signed [boolean]]]]

[datatype
  type-tuple []
  [record
    [tag [exp type-tuple-tag]]
    [types [plist [pmem [wtype-data]]]]]

  wtype-data []
  [union
    [type-error]
    [type-integer]
    [type-tuple]]]

[datatype wtype [] [pmem [wtype-data]]]

[datatype
  wexp-integer []
  [record
    [tag [exp exp-integer-tag]]
    [negative [i32]]
    [value [i64]]
    [type [wtype]]]

  wexp-var []
  [record
    [tag [exp exp-var-tag]]
    [name [text]]]

  wexp-if []
  [record
    [tag [exp exp-if-tag]]
    [condition [pexp]]
    [true [pexp]]
    [false [pexp]]]

  wexp-intrinsic []
  [record
    [tag [exp exp-intrinsic-tag]]
    [iname [text]]
    [arguments [plist [pexp]]]]

  pexp [] [pmem [wexp]]

  wexp []
  [union
    [wexp-integer]
    [wexp-var]
    [wexp-if]
    [wexp-intrinsic]
    ]]

[defn wexp-integer-alloc [i [i64] negative [i32] type [wtype]] [pexp]
  [alloc-init
    [record
      tag [annotation [exp exp-integer-tag] exp-integer-tag]
      negative negative
      value i
      type type]]]

[defn wexp-intrinsic-alloc [name [text] args [plist [pexp]]] [pexp]
  [alloc-init
    [record
      tag [annotation [exp exp-intrinsic-tag] exp-intrinsic-tag]
      iname name
      arguments args]]]

[def type-i32
  [cast [wtype]
    [data mem [record
      tag [annotation [exp type-integer-tag] type-integer-tag]
      n-bytes 4
      signed true]]]]

[def type-i64
  [cast [wtype]
    [data mem [record
      tag [annotation [exp type-integer-tag] type-integer-tag]
      n-bytes 8
      signed true]]]]

[def type-error [cast [wtype] [data mem [record tag [annotation [exp type-error-tag] type-error-tag]]]]]

[def type-tuple-empty
  [cast [wtype]
    [data mem
      [record
        tag [annotation [exp type-tuple-tag] type-tuple-tag]
        types [cast [wtype] [data mem [record size 0]]]]]]]

[defn type-tuple-alloc [types [plist [pmem [wtype-data]]]] [wtype]
  [if [list-empty-is types]
    type-tuple-empty
    [alloc-init
      [record
        tag [annotation [exp type-tuple-tag] type-tuple-tag]
        types types]]]]

[datatype type-kind []
  ; datatype
  ; func
  ; exp type

  [i32]]

[def global-desc-func-tag [i32 2]]

[datatype global-desc-func []
  [record
    [tag [exp global-desc-func-tag]]
    [type-parameters [plist [tuple [text] [type-kind]]]]
    [parameters [plist [tuple [text] [wtype]]]]
    [result [wtype]]
    ; [locals [plist [tuple [text] [wtype]]]]
    [body [plist [pexp]]]]]

[def global-desc-def-tag [i32 3]]

[datatype global-desc-def []
  [record
    [tag [exp global-desc-def-tag]]
    [exp [pexp]]]]

[def global-desc-mutable-tag [i32 4]]

[datatype global-desc-mutable []
  [record
    [tag [exp global-desc-mutable-tag]]
    [exp [pexp]]]]

[datatype global-context-kind []
  [union
    [global-desc-func]
    [global-desc-def]
    [global-desc-mutable]
    ]]

[datatype error []
  [record
    [message [text]]
    [offset [i32]]]]

[datatype global-context []
  [record
    [errors [p-growable-list [error]]]
    [names [p-assoc-list [text] [pmem [global-context-kind]]]]
    [form-offsets [p-assoc-list [pform] [i32]]]
    [exp-types [p-assoc-list [pexp] [wtype]]]
    [exports [p-assoc-list [text] [pmem [global-context-kind]]]]
    ]]

[genfn assoc-list-alloc [k datatype v datatype] [sz [i32]] [p-assoc-list k v]
  [[instantiate growable-list-alloc-init [tuple k v]] sz]]

[defn global-context-alloc [form-offsets [p-assoc-list [pform] [i32]]] [pmem [global-context]]
  [alloc-init
    [record
      errors [[instantiate growable-list-alloc-init [error]] [i32 10]]
      names [[instantiate assoc-list-alloc [text] [pmem [global-context-kind]]] [i32 10]]
      form-offsets form-offsets
      exp-types [[instantiate assoc-list-alloc [pexp] [wtype]] [i32 10]]
      exports [[instantiate assoc-list-alloc [text] [pmem [global-context-kind]]] [i32 10]]]]]

[defn global-context-free [gctx [pmem [global-context]]] [tuple]
  [growable-list-free [deref [pfield gctx errors]]]
  [growable-list-free [deref [pfield gctx names]]]
  [growable-list-free [deref [pfield gctx exp-types]]]
  [free gctx [size-of-type [global-context]]]]

[export global-context-alloc global-context-free]

[datatype local-context-kind []
  [i32]]

[datatype
  local-context [size [exp [i32]]]
  [record
    [context-kind [local-context-kind]]
    [outer [pmem [local-context -sz]]]
    [locals [list-data [tuple [text] [wtype]] size]]]]

[datatype plocal-context [] [pmem [local-context -sz]]]

[def local-context-empty [cast [plocal-context] [i32 0]]]

[defn local-context-alloc [kind [i32] outer [plocal-context] size [i32]] [plocal-context]
  [let
    [p [[instantiate alloc-fn [local-context [exp size]]]]]
    [passign [pfield p context-kind] kind]
    [passign [pfield p outer] outer]
    [passign [pfield [pfield p locals] size] size]
    p]]

[defn locals-lookup [locals [plist [tuple [text] [wtype]]] name [text]] [pmem [wtype]]
  [loop
    [n [list-size locals]
     tuple-array [list-data-array locals]
     i 0]
    [if [lt-s i n]
      [let [ptup [pindex tuple-array i]]
        [if [word-eq [deref [pindex ptup [i32 0]]] name]
          [pindex ptup [i32 1]]
          [continue i [inc i]]]]
      [cast [pmem [wtype]] [i32 0]]]]]

[defn local-context-lookup [lctx [plocal-context] name [text]] [tuple [plocal-context] [wtype]]
  [loop [cur-ctx lctx]
    [let [ptype [locals-lookup [pfield lctx locals] name]]
      [if [is-null ptype]
        [let [outer [deref [pfield lctx outer]]]
          [if [is-null outer]
            [tuple [cast [plocal-context] 0] type-error]
            [continue cur-ctx outer]]]
        [tuple cur-ctx [deref ptype]]]]]]

[defn log-assoc [pglist [p-assoc-list [pexp] [wtype]]] [tuple]
  [log-i32 [deref [pfield pglist size]]]
  [loop
    [cur-size [deref [pfield pglist size]]
     i 0]
    [when [lt-s i cur-size]
      [log-i32 [deref [pindex [pindex [deref [pfield pglist parray]] i] [i32 0]]]]
      [log-i32 [deref [pindex [pindex [deref [pfield pglist parray]] i] [i32 1]]]]
      [continue i [inc i]]]]]

[defn push-error [gctx [pmem [global-context]] form [pform] msg [text]] [tuple]
  [let
    [errors [deref [pfield gctx errors]]
     p-offset [assoc-list-get-value [deref [pfield gctx form-offsets]] form]]
    [when [is-null p-offset]
      [log-i32 [list-size [deref [pfield gctx form-offsets]]]]
      [log-assoc [deref [pfield gctx form-offsets]]]
      [log-abort [text 'push-error form not found in form-offsets']]]
    [growable-list-push errors
      [record
        message msg
        offset [deref p-offset]]]]]

[defn is-digit [cc [i64]] [i32]
  [is-between-inclusive [i64 48] cc [i64 57]]]

[defn form-word-get-report [gctx [pmem [global-context]] pf [pform]] [text]
  [if [form-word-is pf]
    [let [pfw [cast [pmem [form-word]] pf]]
      [deref [pfield pfw pword]]]
    [do
      [push-error gctx pf [static-list [text 'not a form-list']]]
      [static-list [text '']]]]]

[defn form-list-get-report [gctx [pmem [global-context]] pf [pform]] [plist [pform]]
  [if [form-list-is pf]
    [let [pfl [cast [pmem [form-list]] pf]]
      [deref [pfield pfl forms]]]
    [do
      [push-error gctx pf [static-list [text 'not a form-list']]]
      [cast [plist [pform]] empty-list]]]]

; https://github.com/skeeto/scratch/blob/master/parsers/strtonum.c
[defn dec-word-to-i64-signed [gctx [pmem [global-context]] word-form [pform]] [tuple [i32] [i64]]
  [let
    [w [form-word-get-report gctx word-form]
     ws [list-size w]
     first [list-get w 0]]
    [if [intrinsic i64.eq first [i64 45]]
      [tuple
        true
        [loop [i 1 res [i64 0]]
          [if [lt-s i ws]
            [let [cc [list-get w i]]
              [if [not [is-digit cc]]
                [do
                  [push-error gctx word-form [static-list [text 'not a valid digit']]]
                  [i64 0]]
                [let
                  [n [intrinsic i64.mul res [i64 10]]
                   s [intrinsic i64.sub n [intrinsic i64.sub cc [i64 48]]]]
                  [if [intrinsic i64.lt-s n s]
                    [do
                      [push-error gctx word-form [static-list [text 'i64 underflow']]]
                      [i64 0]]
                    [continue
                      res s
                      i [inc i]]]]]]
            res]]]
      [tuple
        false
        [loop [i 0 res [i64 0]]
          [if [lt-s i ws]
            [let [cc [list-get w i]]
              [if [not [is-digit cc]]
                [do
                  [push-error gctx word-form [static-list [text 'not a valid digit']]]
                  [i64 0]]
                [let
                  [n [intrinsic i64.mul res [i64 10]]
                  s [intrinsic i64.add n [intrinsic i64.sub cc [i64 48]]]]
                  [if [intrinsic i64.lt-u s n]
                    [do
                      [push-error gctx word-form [static-list [text 'i64 overflow']]]
                      [i64 0]]
                    [continue
                      res s
                      i [inc i]]]]]]
            res]]]]]]

[def i32-singleton
  [static-list [array [wtype] type-i32]]]

[def i32i32
  [static-list [array [wtype] type-i32 type-i32]]]

[datatype intrinsic-info []
  [record
    [name [text]]
    [parameters [plist [wtype]]]
    [result [wtype]]
    ]]

[def intrinsics
  [static-list
    [array [intrinsic-info]
      [record name [static-list [text i32.eqz]] parameters i32-singleton result type-i32]

      [record name [static-list [text i32.eq]] parameters i32i32 result type-i32]
      [record name [static-list [text i32.ne]] parameters i32i32 result type-i32]

      ; lt gt and friends...

      [record name [static-list [text i32.clz]] parameters i32-singleton result type-i32]
      [record name [static-list [text i32.ctz]] parameters i32-singleton result type-i32]
      [record name [static-list [text i32.popcnt]] parameters i32-singleton result type-i32]

      [record name [static-list [text i32.add]] parameters i32i32 result type-i32]
      [record name [static-list [text i32.sub]] parameters i32i32 result type-i32]
      [record name [static-list [text i32.mul]] parameters i32i32 result type-i32]

      [record name [static-list [text i32.and]] parameters i32i32 result type-i32]
      [record name [static-list [text i32.or]] parameters i32i32 result type-i32]
      [record name [static-list [text i32.xor]] parameters i32i32 result type-i32]

      [record name [static-list [text i32.shl]] parameters i32i32 result type-i32]
      ; [record name [static-list [text i32.shr-s]] parameters i32i32 result type-i32]
      ; [record name [static-list [text i32.shr-u]] parameters i32i32 result type-i32]

      [record name [static-list [text i32.rotl]] parameters i32i32 result type-i32]
      [record name [static-list [text i32.rotr]] parameters i32i32 result type-i32]


      ]]]

[defn get-intrinsic-by-name [name [text]] [pmem [intrinsic-info]]
  [loop
    [cur-size [list-size intrinsics]
     arr [list-data-array intrinsics]
     i 0]
    [if [lt-s i cur-size]
      [let [pintrin [pindex arr i]]
        [if [word-eq [deref [pfield pintrin name]] name]
          pintrin
          [continue i [inc i]]]]
      [cast [pmem [intrinsic-info]] [i32 0]]]]]

[defn type-of [gctx [pmem [global-context]] e [pexp]] [wtype]
  [let [tag [deref [pfield e tag]]]
    [ifs
      [eq tag exp-integer-tag]
      [let
        [exp [cast [pmem [wexp-integer]] e]]
        [deref [pfield exp type]]]

      [eq tag exp-intrinsic-tag]
      [let
        [intr [cast [pmem [wexp-intrinsic]] e]
         intrinsic-info [get-intrinsic-by-name [deref [pfield intr iname]]]]
        [when [is-null intrinsic-info]
          [log-text [deref [pfield intr iname]]]
          [log-abort [text 'type-of not found intrinsic-info']]]
        [deref [pfield intrinsic-info result]]]

      [let [exp-types [deref [pfield gctx exp-types]]
            annotated-type [assoc-list-get-value exp-types e]]
        [if [is-null annotated-type]
          [do
            [log-i32 tag]
            [log-i32 annotated-type]
            [log-i32 [list-size exp-types]]
            [log-abort [text 'type-of not annotated']]]
          [deref annotated-type]]]]]]

[defn type-ofs [gctx [pmem [global-context]] es [plist [pexp]]] [wtype]
  [if [list-empty-is es]
    type-tuple-empty
    [type-of gctx [list-last es]]]]

[defn type-assignable [dst [wtype] src [wtype]] [i32]
  [eq dst src]]

[defn common-super-type [t1 [wtype] t2 [wtype]] [wtype]
  [if [eq t1 t2]
    t1
    [log-abort [text 'common-super-type different types']]]]

[defn annotate [gctx [pmem [global-context]] exp [pexp] t [wtype]] [tuple]
  [assoc-list-push [deref [pfield gctx exp-types]] exp t]]

[defn primitive-int-name-to-signed-size [fw [text]] [tuple [i32] [i64]]
  [ifs
    [word-eq fw [static-list [text i8]]]
    [tuple true [i64 1]]

    [word-eq fw [static-list [text u8]]]
    [tuple false [i64 1]]

    [word-eq fw [static-list [text i16]]]
    [tuple true [i64 2]]

    [word-eq fw [static-list [text u16]]]
    [tuple false [i64 2]]

    [word-eq fw [static-list [text i32]]]
    [tuple true [i64 4]]

    [word-eq fw [static-list [text u32]]]
    [tuple false [i64 4]]

    [word-eq fw [static-list [text i64]]]
    [tuple true [i64 8]]

    [word-eq fw [static-list [text u64]]]
    [tuple false [i64 8]]

    [do
      [log-text fw]
      [log-abort [text 'primitive-int-name-to-signed-size unknown word']]]]]

[defn form-to-exp [gctx [pmem [global-context]] lctx [plocal-context] f [pmem [form]]] [pexp]
  [ifs
    [form-word-is f]
    [let
      [w [form-word-get-report gctx f]
       [found-ctx lvar-type] [local-context-lookup lctx w]
       var-type
        [if [ne found-ctx local-context-empty]
          lvar-type
          [let [ppdesc [assoc-list-get-value [deref [pfield gctx names]] w]]
            [if [is-null ppdesc]
              [do
                [log-text w]
                [log-assoc [pfield found-ctx locals]]
                [push-error gctx f [static-list [text 'variable not found']]]
                type-error]
              [let [pdesc [deref ppdesc]]
                [if [eq [deref [pfield pdesc tag]] global-desc-def-tag]
                  [type-of gctx [deref [pfield [cast [pmem [global-desc-def]] pdesc] exp]]]
                  [do
                    [push-error gctx f [static-list [text 'variable not a def']]]
                    type-error]]]]]]
       exp
       [alloc-init
        [record
          tag [annotation [exp exp-var-tag] exp-var-tag]
          name w]]]
      [annotate gctx exp var-type]
      exp]

    [form-list-is f]
    [let [fl [form-list-get-report gctx f]
          n [list-size fl]
          n-args [dec n]]
      [if [eq n 0]
        [do
          [push-error gctx f [static-list [text 'empty list not allowed']]]
          [cast [pexp] null]]
      [let [fw [form-word-get-report gctx [list-get fl 0]]]
        [ifs
          [or
            [word-eq fw [static-list [text i8]]]  [word-eq fw [static-list [text u8]]]
            [word-eq fw [static-list [text i16]]] [word-eq fw [static-list [text u16]]]
            [word-eq fw [static-list [text i32]]] [word-eq fw [static-list [text u32]]]
            [word-eq fw [static-list [text i64]]] [word-eq fw [static-list [text u64]]]]
          [let
            [[signed byte-size] [primitive-int-name-to-signed-size fw]
              bit-size [intrinsic i64.mul byte-size [i64 8]]
             [negative value]
             [if [ne n-args 1]
              [do
                [push-error gctx f [static-list [text 'expects one argument']]]
                [tuple [i32 0] [i64 0]]]
              [dec-word-to-i64-signed gctx [list-get fl 1]]]
              type
              [ifs
                [word-eq fw [static-list [text i32]]] type-i32
                [word-eq fw [static-list [text i64]]] type-i64
                [do
                  [log-text fw]
                  [log-abort [text 'form-to-exp primitive-int-name-to-signed-size unknown word']]]]]
            [if signed
              [let
                [pow2 [intrinsic i64.shl [i64 1] [intrinsic i64.sub bit-size [i64 1]]]
                 lower [intrinsic i64.sub [i64 0] pow2]
                 upper [intrinsic i64.sub pow2 [i64 1]]]
                [when [intrinsic i64.lt-s value lower]
                  [push-error gctx f [static-list [text 'signed value out of range, too low']]]]
                [when [intrinsic i64.lt-s upper value]
                  [push-error gctx f [static-list [text 'signed value out of range, too high']]]]]
              [do
                [when negative
                  [push-error gctx f [static-list [text 'negative value for unsigned type']]]]
                [let [unsigned-upper [intrinsic i64.shl [i64 1] bit-size]]
                  [when [intrinsic i64.le-s unsigned-upper value]
                    [push-error gctx f [static-list [text 'unsigned value out of range']]]]]]]
            [wexp-integer-alloc value negative type]]

          [word-eq fw [static-list [text if]]]
          [do
            [if [ne n-args 3]
              [log-abort [text 'form-to-exp if expects 3 arguments']]]
            [let
              [c [form-to-exp gctx lctx [list-get fl 1]]
               t [form-to-exp gctx lctx [list-get fl 2]]
               e [form-to-exp gctx lctx [list-get fl 3]]
               if-exp
               [alloc-init
                [record
                  tag [annotation [exp exp-if-tag] exp-if-tag]
                  condition c
                  true t
                  false e]]]
              [when [not [type-assignable type-i32 [type-of gctx c]]]
                [push-error gctx [list-get fl 1] [static-list [text 'if condition not i32']]]]
              [annotate gctx if-exp [common-super-type [type-of gctx t] [type-of gctx e]]]
              if-exp]]

          [word-eq fw [static-list [text intrinsic]]]
          [do
            [if [eq n-args 0]
              [log-abort [text 'form-to-exp intrinsic expects at least a name argument']]]
            [let
              [name [form-word-get-report gctx [list-get fl 1]]
               intrinsic-info [get-intrinsic-by-name name]
               n-intrinsic-args [dec n-args]
               args [[instantiate list-alloc [pexp]] n-intrinsic-args]
               params
               [if [is-null intrinsic-info]
                [do
                  [push-error gctx [list-get fl 1] [static-list [text 'intrinsic not found']]]
                  [cast [plist [wtype]] empty-list]]
                [let [params [deref [pfield intrinsic-info parameters]]]
                  [when [ne [list-size params] n-intrinsic-args]
                    [push-error gctx f [static-list [text 'form-to-exp intrinsic expects the right number of arguments']]]]
                  params]]]
              [loop [i 2]
                [when [lt-s i n]
                  [list-set args [sub i 2] [form-to-exp gctx lctx [list-get fl i]]]
                  [continue i [inc i]]]]
              [loop [i 0 nn [list-size params]]
                [when [lt-s i nn]
                  [continue i [inc i]]]]
              [loop [i 0 n-to-check [min [list-size params] n-intrinsic-args]]
                [when [lt-s i n-to-check]
                  [let
                    [param [list-get params i]
                     arg [list-get args i]
                     targ [type-of gctx arg]]
                    [when [not [type-assignable param targ]]
                      [log-ar [text 'i32 is']]
                      [log-i32 type-i32]
                      [log-ar [text 'parameter is']]
                      [log-i32 param]
                      [log-ar [text 'arg is']]
                      [log-i32 targ]
                      [push-error gctx [list-get fl [inc i]] [static-list [text 'intrinsic argument type mismatch']]]]
                    ; todo check type
                    [continue i [inc i]]]]]
              [wexp-intrinsic-alloc name args]]]

          [do
            [log-text fw]
            [log-abort [text 'form-to-exp unknown form-list case']]]]]]]

    [log-abort [text 'form-to-exp unknown form case']]]]

[defn form-to-type [gctx [pmem [global-context]] type-kind-env [p-assoc-list [text] [type-kind]] f [pmem [form]]] [wtype]
  [ifs
    [form-word-is f]
    [let
      [var-name [form-word-get f]
       kind [assoc-list-get-value type-kind-env var-name]]
      [when [is-null kind]
        [log-assoc type-kind-env]
        [log-text var-name]
        [push-error gctx f [static-list [text 'unknown type var']]]]
      type-error]

    [form-list-is f]
    [let [fl [form-list-get-report gctx f]
          n [list-size fl]
          n-args [dec n]]
      [if [eq n 0]
        [log-abort [text 'form-to-type empty list not allowed']]]
      [let [fw [form-word-get-report gctx [list-get fl 0]]]
        [ifs
          [word-eq fw [static-list [text i32]]]
          [do
            [if [ne n-args 0]
              [log-abort [text 'form-to-type i32 expects no arguments']]]
            type-i32]

          [word-eq fw [static-list [text i64]]]
          [do
            [if [ne n-args 0]
              [log-abort [text 'form-to-type i64 expects no arguments']]]
            type-i64]

          [log-abort [text 'form-to-type unknown form-list case']]]]]

    [log-abort [text 'form-to-type unknown form case']]]]

[defn half [i [i32]] [i32] [intrinsic i32.shr-s i 1]]

[def context-kind-func [i32 1]]

[defn parameter-forms-to-local-context
  [gctx [pmem [global-context]]
   type-kind-env [p-assoc-list [text] [type-kind]]
   param-list [plist [pform]]] [plocal-context]
  [let
    [n-of-param-forms [list-size param-list]
     lctx [local-context-alloc context-kind-func local-context-empty [half n-of-param-forms]]
     locals-array [list-data-array [pfield lctx locals]]]
    [if [is-odd n-of-param-forms]
      [push-error gctx [list-get param-list [dec n-of-param-forms]]
        [static-list [text 'form-to-top defn expects a parameter list with names and type pairs']]]]
    [loop [i 0 ti 0]
      [when [lt-s i n-of-param-forms]
        [let
          [lname-form [list-get param-list i]
           lname [form-word-get lname-form]
           [found-ctx lvar-type] [local-context-lookup lctx lname]
           ltype [form-to-type gctx type-kind-env [list-get param-list [inc i]]]]
          [if [ne found-ctx local-context-empty]
            [push-error gctx lname-form [static-list [text 'name already defined']]]]
          [passign
            [pindex locals-array ti]
            [tuple lname ltype]]
          [continue i [add 2 i] ti [inc ti]]]]]
    lctx]]

[defn forms-to-type-params [gctx [pmem [global-context]] param-list [plist [pform]]] [plist [tuple [text] [type-kind]]]
  [let
    [n-of-param-forms [list-size param-list]
     result [[instantiate list-alloc [tuple [text] [type-kind]]] [half n-of-param-forms]]
     darray [list-data-array result]]
    [if [is-odd n-of-param-forms]
      [push-error gctx [list-get param-list [dec n-of-param-forms]]
        [static-list [text 'form-to-top defn expects a parameter list with names and type pairs']]]]
    [loop [i 0 ti 0]
      [when [lt-s i [dec n-of-param-forms]]
        [let
          [name-form [list-get param-list i]
           kind-form [list-get param-list [inc i]]
           name [form-word-get-report gctx name-form]
           kind
           [ifs
            [form-word-is kind-form]
            [let [kind-w [form-word-get kind-form]]
              [ifs [word-eq kind-w [static-list [text datatype]]]
                [i32 1]
                [log-abort [text 'forms-to-type-params unknown type-kind']]]]

            [log-abort [text 'forms-to-type-params unknown type-kind']]]]
          [passign
            [pindex darray ti]
            [tuple name kind]]
          [continue i [add 2 i] ti [inc ti]]]]]
    [cast [plist [tuple [text] [type-kind]]] empty-list]]]

[defn forms-to-exps [gctx [pmem [global-context]]
                     lctx [plocal-context]
                     fl [plist [pform]]
                     start [i32]
                     end [i32]] [plist [pexp]]
  [loop [j start bodies [[instantiate list-alloc [pexp]] [sub end start]]]
    [if [lt-s j end]
      [do
        ; list-map would have been nice here
        [list-set bodies [sub j start] [form-to-exp gctx lctx [list-get fl j]]]
        [continue j [inc j]]]
      bodies]]]

[defn form-to-top [gctx [pmem [global-context]] form [pmem [form]]] [tuple]
  [ifs
    [form-word-is form]
    [do
      [log-abort [text 'form-to-top word case not implemented']]]

    [form-list-is form]
    [let [fl [form-list-get-report gctx form]
          n [list-size fl]
          n-args [dec n]]
      [if [eq n 0]
        [push-error gctx form [static-list [text 'form-to-top empty list not allowed']]]]
      [let [fw [form-word-get-report gctx [list-get fl 0]]]
        [ifs
          [word-eq fw [static-list [text defn]]]
          [do
            [if [lt-s n-args 3]
              [log-abort [text 'form-to-top defn expects at least 3 arguments']]]
            [let
              [name-form [list-get fl 1]
               param-list [form-list-get-report gctx [list-get fl 2]]
               result-type [form-to-type gctx empty-list [list-get fl 3]]
               fname [form-word-get-report gctx name-form]
               lctx [parameter-forms-to-local-context gctx empty-list param-list]
               desc [[instantiate alloc-fn [global-desc-func]]]]
              [if [assoc-list-get-value [deref [pfield gctx names]] fname]
                [push-error gctx name-form [static-list [text 'name already defined']]]]
              [passign desc
                [record
                  tag [annotation [exp global-desc-func-tag] global-desc-func-tag]
                  type-parameters [cast [plist [tuple [text] [wtype]]] empty-list]
                  parameters [pfield lctx locals]
                  result result-type]]
              [assoc-list-push [deref [pfield gctx names]] fname desc]
              [let [bodies [forms-to-exps gctx lctx fl 4 n]]
                [when [not [type-assignable result-type [type-ofs gctx bodies]]]
                  [push-error gctx form [static-list [text 'result type mismatch']]]]
                [passign [pfield desc body] bodies]]]]

          [word-eq fw [static-list [text genfn]]]
          [do
            [if [lt-s n-args 4]
              [log-abort [text 'form-to-top genfn expects at least 4 arguments']]]
            [let
              [name-form [list-get fl 1]
               type-param-list [form-list-get-report gctx [list-get fl 2]]
               param-list [form-list-get-report gctx [list-get fl 3]]
               result-type-form [list-get fl 4]

               fname [form-word-get-report gctx name-form]
               type-param-assoc [forms-to-type-params gctx type-param-list]

               lctx [parameter-forms-to-local-context gctx type-param-assoc param-list]
               desc [[instantiate alloc-fn [global-desc-func]]]
               result-type [form-to-type gctx type-param-assoc result-type-form]]
              [if [assoc-list-get-value [deref [pfield gctx names]] fname]
                [push-error gctx name-form [static-list [text 'name already defined']]]]
              [passign desc
                [record
                  tag [annotation [exp global-desc-func-tag] global-desc-func-tag]
                  type-parameters type-param-assoc
                  parameters [pfield lctx locals]
                  result result-type]]
              [assoc-list-push [deref [pfield gctx names]] fname desc]
              [let [bodies [forms-to-exps gctx lctx fl [i32 5] n]]
                [when [not [type-assignable result-type [type-ofs gctx bodies]]]
                  [push-error gctx form [static-list [text 'result type mismatch']]]]
                [passign [pfield desc body] bodies]]]]

          [word-eq fw [static-list [text def]]]
          [do
            [if [ne n-args 2]
              [log-abort [text 'form-to-top def expects a name and an expression']]]
            [let
              [dname [form-word-get-report gctx [list-get fl 1]]
               exp [form-to-exp gctx local-context-empty [list-get fl 2]]
               def-desc
               [alloc-init
                [record
                  tag [annotation [exp global-desc-def-tag] global-desc-def-tag]
                  exp exp]]]
              [assoc-list-push [deref [pfield gctx names]] dname def-desc]]]

          [word-eq fw [static-list [text mutable]]]
          [do
            [if [ne n-args 2]
              [log-abort [text 'form-to-top mutable expects a name and an expression']]]
            [let
              [dname [form-word-get-report gctx [list-get fl 1]]
               exp [form-to-exp gctx local-context-empty [list-get fl 2]]
               mutable-desc
               [alloc-init
                [record
                  tag [annotation [exp global-desc-mutable-tag] global-desc-mutable-tag]
                  exp exp]]]
              [assoc-list-push [deref [pfield gctx names]] dname mutable-desc]]]

          [word-eq fw [static-list [text datatype]]]
          [do
            [log-abort [text 'form-to-top datatype not implemented']]
          ]

          [word-eq fw [static-list [text import]]]
          [do
            [log-abort [text 'form-to-top import not implemented']]
          ]

          [word-eq fw [static-list [text export]]]
          [do
            [if [lt-s n-args 1]
              [log-abort [text 'form-to-top export expects at least 1 arguments']]]
            [let
              [names [deref [pfield gctx names]]
               exports [deref [pfield gctx exports]]]
              [loop [k 1]
                [when [lt-s k n]
                  [let
                    [ef [list-get fl k]
                     export-name [form-word-get-report gctx ef]
                     edesc [assoc-list-get-value names export-name]]
                    [if [is-null edesc]
                      [push-error gctx ef [static-list [text 'export name not found']]]
                      [assoc-list-push exports export-name [deref edesc]]]
                    [continue k [inc k]]]]]]]

          [do
            [log-text fw]
            [log-abort [text 'form-to-top unknown form-list case']]]]]]

    [log-abort [text 'form-to-top unknown form case']]]]

[export form-to-top]

[defn parse-translate [bytes [text]] [pmem [global-context]]
  [let
    [[forms texts form-offsets] [parse-n bytes]
     gctx [global-context-alloc form-offsets]]
    [loop [i 0 n [list-size forms]]
      [when [lt-s i n]
        [form-to-top gctx [growable-list-get forms i]]
        [continue i [inc i]]]]
    [let [errors [deref [pfield gctx errors]]]
      [loop [j 0 nerr [list-size errors]]
        [when [lt-s j nerr]
          [let [error [growable-list-get-p errors j]]
            [log-text [deref [pfield error message]]]
            [log-i32 [deref [pfield error offset]]]
            [continue j [inc j]]]]]]
    gctx]]

[export parse-translate]

[datatype wasm-type [] [text]]

[def wasm-constant-tag [i32 1]]

[datatype wasm-constant []
  [record
    [tag [exp wasm-constant-tag]]
    [type [wasm-type]]
    [value [text]]]]

[def wasm-intrinsic-tag [i32 2]]
[def wasm-call-tag [i32 3]]
[def wasm-if-tag [i32 4]]
[def wasm-loop-tag [i32 5]]

[datatype
  wasm-intrinsic []
  [record
    [tag [exp wasm-intrinsic-tag]]
    [name [text]]
;    [immediate-arguments [plist [imm-arg]]]
    [arguments [plist [pmem [wasm-exp]]]]]

  wasm-call []
  [record
    [tag [exp wasm-call-tag]]
    [name [text]]
    [arguments [plist [pmem [wasm-exp]]]]]

  wasm-if []
  [record
    [tag [exp wasm-if-tag]]
    [result [plist [wasm-type]]]
    [condition [pmem [wasm-exp]]]
    [true [pmem [wasm-exp]]]
    [false [pmem [wasm-exp]]]]

  wasm-loop []
  [record
    [tag [exp wasm-loop-tag]]
    [label [text]]
    [result [plist [wasm-type]]]
    [body [pmem [wasm-exp]]]]

  wasm-exp []
  [union
    [wasm-constant]
    [wasm-intrinsic]
    [wasm-call]
    [wasm-if]
      ]]

[datatype pwasm-exp [] [pmem [wasm-exp]]]

[defn integer-to-text [n [i64] negative [i32]] [text]
  [if [intrinsic i64.eq n [i64 0]]
    [static-list [text 0]]
    [let [gl [[instantiate growable-list-alloc-init [i64]] [i32 10]]]
      [if negative
        [do
          [loop [cur n]
            [when [intrinsic i64.ne cur [i64 0]]
              [let
                [rem [intrinsic i64.sub [i64 0] [intrinsic i64.rem-s cur [i64 10]]]]
                [growable-list-push gl [intrinsic i64.add [i64 48] rem]]
                [continue cur [intrinsic i64.div-s cur [i64 10]]]]]]
          [growable-list-push gl [i64 45]]]
        [do
          [loop [cur n]
            [when [intrinsic i64.ne cur [i64 0]]
              [let [rem [intrinsic i64.rem-s cur [i64 10]]]
                [growable-list-push gl [intrinsic i64.add [i64 48] rem]]
                [continue cur [intrinsic i64.div-s cur [i64 10]]]]]]
          [growable-list-push gl [i64 43]]]]
      [let
        [rsz [list-size gl]
         result [list-alloc-byte rsz]
         result-data [list-data-array result]]
        [loop [i 0]
          [when [lt-s i rsz]
            [passign [pindex result-data i] [deref [pindex [deref [pfield gl parray]] [dec [sub rsz i]]]]]
            [continue i [inc i]]]]
        [growable-list-free gl]
        result]]]]

[def i32-text [static-list [text 'i32']]]
[def i64-text [static-list [text 'i64']]]
[def f32-text [static-list [text 'f32']]]
[def f64-text [static-list [text 'f64']]]

[defn type-to-wasm-type [t [wtype]] [wasm-type]
  [ifs
    [eq t type-i32]
    i32-text

    [eq t type-i64]
    i64-text

    [let [tag [deref [pfield t tag]]]
      [ifs
        [eq tag type-integer-tag]
        [let
          [int-type [cast [pmem [type-integer]] t]]
          [when [not [deref [pfield int-type signed]]]
            [log-i32 [deref [pfield int-type n-bytes]]]
            [log-abort [text 'type-to-wasm-type unsigned integer not supported']]]
          [ifs
            [eq [deref [pfield int-type n-bytes]] [i32 4]]
            i32-text

            [eq [deref [pfield int-type n-bytes]] [i32 8]]
            i64-text

            [log-abort [text 'type-to-wasm-type integer not supported']]]]
          [log-abort [text 'type-to-wasm-type non-integer not supported']]]]]]

[defn flatten-wuns-type [gl [p-growable-list [wtype]] t [wtype]] []
  [let [tag [deref [pfield t tag]]]
    [if [eq tag type-tuple-tag]
      [let
        [types [deref [pfield [cast [pmem [type-tuple]] t] types]]
         n-types [list-size types]]
        [loop [i 0]
          [when [lt-s i n-types]
            [flatten-wuns-type gl [list-get types i]]
            [continue i [inc i]]]]]
      [growable-list-push gl t]]]]

[defn type-to-wasm-types [t [wtype]] [plist [wasm-type]]
  [let
    [types [[instantiate growable-list-alloc-init [wtype]] [i32 8]]
     [] [flatten-wuns-type types t]
     n-types [list-size types]
     wasm-types [[instantiate list-alloc [wasm-type]] n-types]]
    [loop [i 0]
      [when [lt-s i n-types]
        [list-set wasm-types i [type-to-wasm-type [growable-list-get types i]]]
        [continue i [inc i]]]]
    [growable-list-free types]
    wasm-types]]

[defn exp-to-wasm-exp [gctx [pmem [global-context]] exp [pexp]] [pwasm-exp]
  [let [tag [deref [pfield exp tag]]]
    [ifs
      [eq tag exp-integer-tag]
      [let
        [iexp [cast [pmem [wexp-integer]] exp]
         negative [deref [pfield iexp negative]]
         value [deref [pfield iexp value]]
         type [deref [pfield iexp type]]
         txt [integer-to-text value negative]]
        [cast [pwasm-exp]
          [alloc-init
            [record
              tag [annotation [exp wasm-constant-tag] wasm-constant-tag]
              type type
              value txt]]]]

      [eq tag exp-intrinsic-tag]
      [let
        [iexp [cast [pmem [wexp-intrinsic]] exp]
         iname [deref [pfield iexp iname]]
         arguments [deref [pfield iexp arguments]]
         nargs [list-size arguments]
         warguments [[instantiate list-alloc [pmem [wasm-exp]]] nargs]]
        [loop [i 0]
          [when [lt-s i nargs]
            [list-set warguments i [exp-to-wasm-exp gctx [list-get arguments i]]]
            [continue i [inc i]]]]
        [cast [pwasm-exp]
          [alloc-init
            [record
              tag [annotation [exp wasm-intrinsic-tag] wasm-intrinsic-tag]
              name iname
              arguments warguments]]]]

      [eq tag exp-if-tag]
      [let
        [iexp [cast [pmem [wexp-if]] exp]
         condition [deref [pfield iexp condition]]
         true [deref [pfield iexp true]]
         false [deref [pfield iexp false]]
         econd [exp-to-wasm-exp gctx condition]
         etrue [exp-to-wasm-exp gctx true]
         efalse [exp-to-wasm-exp gctx false]
         tif [type-of gctx exp]]
        [cast [pwasm-exp]
          [alloc-init
            [record
              tag [annotation [exp wasm-if-tag] wasm-if-tag]
              result [type-to-wasm-types tif]
              condition econd
              true etrue
              false efalse]]]]

        [do
          [log-i32 tag]
          [log-abort [text 'exp-to-wasm-exp unknown tag']]]]]]

[datatype writer []
  [p-growable-list [text]]]

[defn write-text [writer [writer] txt [text]] [tuple]
  [growable-list-push writer txt]]

[defn writer-total-size [writer [writer]] [i32]
  [loop [i 0 n [list-size writer] total-size 0]
    [if [lt-s i n]
      [continue
        total-size [add total-size [list-size [growable-list-get writer i]]]
        i [inc i]]
      total-size]]]

[defn writer-to-text [writer [writer]] [text]
  [let
    [total-size [writer-total-size writer]
     result [list-alloc-byte total-size]
     result-data [list-data-array result]]
    [loop [i 0 n [list-size writer] offset 0]
      [if [lt-s i n]
        [let
          [txt [growable-list-get writer i]
           txt-size [list-size txt]]
          [memory-copy [pindex result-data offset] [list-data-array txt] txt-size]
          [continue
            offset [add offset txt-size]
            i [inc i]]]]]
    result]]

[def lparen [static-list [text '(']]]
[def rparen [static-list [text ')']]]
[def dollar [static-list [text '$']]]
[def dquote [static-list [text '"']]]
[def space [static-list [text ' ']]]
[def newline [static-list [array [u8] [u8 10]]]]

[defn paren [writer [writer] txt [text]] [tuple]
  [write-text writer lparen]
  [write-text writer txt]
  [write-text writer rparen]]

[defn write-dollar-id [writer [writer] txt [text]] [tuple]
  [write-text writer dollar]
  [write-text writer txt]]

[defn write-results [writer [writer] results [plist [wasm-type]]] []
  [let
    [n-results [list-size results]]
    [when [ne n-results 0]
      [write-text writer space]

      [write-text writer lparen]
      [write-text writer [static-list [text result]]]
      [loop [i 0]
        [when [lt-s i n-results]
          [write-text writer space]
          [write-text writer [list-get results i]]
          [continue i [inc i]]]]

      [write-text writer rparen]]]]

[defn write-wasm-exp [writer [writer] exp [pwasm-exp]] [tuple]
  [write-text writer lparen]
  [let [tag [deref [pfield exp tag]]]
    [ifs
      [eq tag wasm-constant-tag]
      [let [cexp [cast [pmem [wasm-constant]] exp]]
        [write-text writer [type-to-wasm-type [deref [pfield cexp type]]]]
        [write-text writer [static-list [text '.const']]]
        [write-text writer space]
        [write-text writer [deref [pfield cexp value]]]]

      [eq tag wasm-intrinsic-tag]
      [let
        [cexp [cast [pmem [wasm-intrinsic]] exp]
         args [deref [pfield cexp arguments]]]
        [write-text writer [deref [pfield cexp name]]]
        [loop [i 0 n [list-size args]]
          [when [lt-s i n]
            [write-text writer space]
            [write-wasm-exp writer [list-get args i]]
            [continue i [inc i]]]]]

      [eq tag wasm-if-tag]
      [let
        [cexp [cast [pmem [wasm-if]] exp]]
        [write-text writer [static-list [text 'if']]]

        [write-results writer [deref [pfield cexp result]]]

        [write-text writer space]
        [write-wasm-exp writer [deref [pfield cexp condition]]]
        [write-text writer space]

        [write-text writer lparen]
        [write-text writer [static-list [text then]]]
        [write-text writer space]
        [write-wasm-exp writer [deref [pfield cexp true]]]
        [write-text writer rparen]

        [write-text writer space]

        [write-text writer lparen]
        [write-text writer [static-list [text else]]]
        [write-text writer space]
        [write-wasm-exp writer [deref [pfield cexp false]]]
        [write-text writer rparen]
        ]

      [log-abort [text 'write-wasm-exp unknown tag']]]]

  [write-text writer rparen]]

[def indent [static-list [text '  ']]]

[defn write-func-desc-to-wasm [writer [writer] gctx [pmem [global-context]] fname [text] func-desc [pmem [global-desc-func]]] []
  [write-text writer lparen]
  [write-text writer [static-list [text func]]]
  [write-text writer space]

  [write-dollar-id writer fname]

  [let
    [parameters [deref [pfield func-desc parameters]]
     result [deref [pfield func-desc result]]
     body [deref [pfield func-desc body]]]
    [loop [i 0 n [list-size parameters]]
      [when [lt-s i n]
        [let
          [pp [list-get-p parameters i]
           wuns-type [deref [pindex pp [i32 1]]]
           wasm-types [type-to-wasm-types wuns-type]]
          [when [ne [list-size wasm-types] 1]]
          [write-text writer space]

          [write-text writer lparen]

          [write-text writer [static-list [text param]]]
          [write-text writer space]

          [write-dollar-id writer [deref [pindex pp [i32 0]]]]
          [write-text writer space]

          [write-text writer [type-to-wasm-type [deref [pindex pp [i32 1]]]]]

          [write-text writer rparen]]
        [continue i [inc i]]]]

    [write-results writer [type-to-wasm-types result]]

    ; todo locals here...

    [when [lt-s 1 [list-size body]]
      [log-abort [text 'write-func-desc-to-wasm multiple bodies not yet supported']]]
    [loop [i 0 n [list-size body]]
      [when [lt-s i n]
        [write-text writer newline]

        [write-wasm-exp writer [exp-to-wasm-exp gctx [list-get body i]]]
        [continue i [inc i]]]]

        ]

  [write-text writer rparen]
  [write-text writer newline]
  ]

[defn write-export [writer [writer] export-name [text] export-kind [text] internal-name [text]] [tuple]
  [write-text writer lparen]
  [write-text writer [static-list [text 'export']]]
  [write-text writer space]

  [write-text writer dquote]
  [write-text writer export-name]
  [write-text writer dquote]

  [write-text writer space]

  [write-text writer lparen]

  [write-text writer export-kind]

  [write-text writer space]

  [write-dollar-id writer internal-name]

  [write-text writer rparen]
  [write-text writer rparen]
  [write-text writer newline]
  ]

[defn write-global-context [writer [writer] gctx [pmem [global-context]]] []
  [let
    [names [deref [pfield gctx names]]
     exports [deref [pfield gctx names]]]
    [loop
      [n [list-size names]
       i 0]
      [when [lt-s i n]
        [let
          [assoc-pair [growable-list-get-p names i]
          name [deref [pindex assoc-pair [i32 0]]]
          desc [deref [pindex assoc-pair [i32 1]]]
          tag [deref [pfield desc tag]]]
          [ifs
            [eq tag global-desc-func-tag]
            [write-func-desc-to-wasm writer gctx name desc]

            [log-ar [text 'write-global-context ignoring unknown tag']]]
          [continue i [inc i]]]]]
    [loop
      [n [list-size exports]
       i 0]
      [when [lt-s i n]
        [let
          [assoc-pair [growable-list-get-p exports i]
           name [deref [pindex assoc-pair [i32 0]]]
           desc [deref [pindex assoc-pair [i32 1]]]
           tag [deref [pfield desc tag]]]
          [ifs
            [eq tag global-desc-func-tag]
            [write-export writer name [static-list [text 'func']] name]

            [log-ar [text 'write-global-context export ignoring unknown tag']]]
          [continue i [inc i]]]]]

          ]]

[defn parse-translate-to-wat [bytes [text]] [text]
  [let
    [gctx [parse-translate bytes]
     errors [deref [pfield gctx errors]]]
    [when [ne [list-size errors] 0]
      [log-i32 [list-size errors]]
      [log-abort [text 'parse-translate-to-wat there were errors']]]
    [let
      [writer [[instantiate growable-list-alloc-init [text]] [i32 128]]]
      [write-global-context writer gctx]
      [global-context-free gctx]
      [let [wat [writer-to-text writer]]
        [growable-list-free writer]
        wat]]]]

[export parse-translate-to-wat]

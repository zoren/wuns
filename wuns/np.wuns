[def -2 [quote -2]]
[def -1 [quote -1]]
[def 0 [quote 0]]
[def 1 [quote 1]]
[def 2 [quote 2]]
[def 3 [quote 3]]
[def 4 [quote 4]]

[def defmacro
  [func defmacro [name params .. forms]
    [list [quote let] [list]
      [list [quote def] name [concat [list [quote func] name params] forms]]
      [list [quote set-meta] [list [quote var] [list [quote quote] name]]
        [quote transient-kv-map [quote is-macro] 1]]]]]

[set-meta [var [quote defmacro]] [transient-kv-map [quote is-macro] 1]]

[defmacro defn [name params .. forms]
  [list [quote def] name [concat [list [quote func] name params] forms]]]

[defmacro comment [.. -] [list]]

[defn inc [x] [add x 1]]

[defn dec [x] [sub x 1]]

[defn not [c] [eq c 0]]

[defn is-zero [x] [eq x 0]]

[defn is-odd [x] [bitwise-and x 1]]

[defn is-even [x] [not [is-odd x]]]

[defn is-negative [x] [lt x 0]]

[defn first [l] [at l 0]]
[defn second [l] [at l 1]]
[defn third [l] [at l 2]]
[defn fourth [l] [at l 3]]
[defn last [l] [at l -1]]

[defn rest [l]
  [slice l 1 [size l]]]

[defmacro do [.. forms]
  [concat [quote [let []]] forms]]

[defmacro when [cond .. forms]
  [list
    [quote if] cond
    [concat [quote [do]] forms]
    [list]]]

[defn quote-list [lmsg]
  [let [msg [mutable-list [quote list]]]
    [loop [i 0]
      [when [lt i [size lmsg]]
        [let [e [at lmsg i]]
          [push msg [if [is-word e] [list [quote quote] e] e]]
          [continue i [inc i]]]]]
    [persistent-array msg]]]

[defmacro log-abort [.. lmsg]
  [list [quote let] [list]
    [list [quote log] [quote-list lmsg]]
    [quote [abort]]]]

[defmacro assert [cond .. lmsg]
  [list [quote when] [list [quote not] cond]
    [list [quote log] [quote-list lmsg]]
    [quote [unreachable]]]]

[defn identity [x] x]

[defmacro ifs [.. clauses]
  [let [s [size clauses]]
    [assert [not [is-zero s]] ifs requires an odd number of arguments]
    [if [eq s 1]
      [first clauses]
      [list [quote if] [first clauses]
        [second clauses]
        [concat [quote [ifs]] [slice clauses 2 [size clauses]]]]]]]

[defmacro and [.. clauses]
  [let [s [size clauses]]
    [ifs
      [is-zero s]
      1

      [eq s 1]
      [first clauses]

      [list [quote if] [first clauses]
        [concat [quote [and]] [rest clauses]]
        0]]]]

[defmacro or [.. clauses]
  [let [s [size clauses]]
    [ifs
      [is-zero s]
      0

      [eq s 1]
      [first clauses]

      [list [quote if] [first clauses]
        1
        [concat [quote [or]] [rest clauses]]]]]]

[def word-counter [atom 0]]

[defn genword []
  [let [c [atom-get word-counter]]
    [if [is-i32 c]
      [atom-set word-counter [inc c]]
      [do [log [quote not int]]
        [abort]]]
    [concat-words [list [quote genword] c]]]]

[defn for-func [iw start increment end forms]
  [assert [gt increment 0] for-func increment must be positive]
  [let [ew [genword]]
    [list
      [quote let] [list ew end]
      [list
        [quote loop] [list iw start]
        [list [quote if]
          [list [quote lt] iw ew]
          [concat [quote let []] forms [list [list [quote continue] iw [list [quote add] iw increment]]]]
          [list]]]]]]

[defmacro for [iw start end .. forms]
  [for-func iw start 1 end forms]]

[defmacro for-inc [iw start increment end .. forms]
  [for-func iw start increment end forms]]

[defmacro for-each [element-var collection .. forms]
  [assert [is-word element-var] for-each requires a word as the first argument]
  [let [iteration-var [genword]
        col-var [genword]
        col-size-var [genword]]
    [list
      [quote let] [list col-var collection col-size-var [list [quote size] col-var]]
      [list
        [quote loop] [list iteration-var 0]
        [list [quote if]
          [list [quote lt] iteration-var col-size-var]
          [concat [quote [let]]
            [list [list element-var [list [quote at] col-var iteration-var]]]
            forms
            [list [list [quote continue] iteration-var [list [quote add] iteration-var 1]]]]
          [list]]]]]]

[defmacro kv-map [.. entries]
  [let [ml [mutable-list]]
    [push ml [quote transient-kv-map]]
    [for-inc i 0 2 [size entries]
      [push ml
        [let [k [at entries i]]
                [if [is-word k]
                  [list [quote quote] k]
                  k]]]
      [push ml [at entries [inc i]]]]
    [concat [quote [persistent-kv-map]] [list ml]]]]

[defmacro getq [m w]
  [list [quote get] m [list [quote quote] w]]]

[defmacro hasq [m w]
  [list [quote has] m [list [quote quote] w]]]

[defmacro setq [m w v]
  [list [quote set] m [list [quote quote] w] v]]

[import env mem [memory 0]]

[defmacro s32 [addr val] [list [quote i32.store] 0 2 0 addr val]]
[defmacro s32-offset [offset addr val] [list [quote i32.store] 0 2 offset addr val]]
[defmacro l32 [addr] [list [quote i32.load] 0 2 0 addr]]
[defmacro l32-offset [offset addr] [list [quote i32.load] 0 2 offset addr]]

[defn bump-alloc-init []
  [if [eq [l32 [const 0]] [const 0]]
    [s32 [const 0] [const 16]]
    [unreachable]]]

[defn bump-free [p size]
  [let [current-top [l32 [const 0]]
        popped-top [sub current-top size]]
    [if [eq popped-top p]
      [s32 [const 0] popped-top]
      [unreachable]]]]

[defn try-bump-free [p size] []
  [let [current-top [l32 [const 0]]
        popped-top [sub current-top size]]
    [when [eq popped-top p]
      [s32 [const 0] popped-top]]]]

[defn mk-i32 [x] [list [quote i32.const] x]]

[def mem-alloc-size 4]

[defmacro not-is-address-aligned [x]
  [list [quote bitwise-and] [mk-i32 [dec mem-alloc-size]] x]]

[comment maybe memset newly allocated memory to zero]
[defn bump-alloc [size]
  [if [le size [i32.const 0]] [unreachable]]
  [if [gt size [i32.const 65536]] [unreachable]]
  [if [not-is-address-aligned size] [unreachable]]
  [let
    [current-top [l32 [i32.const 0]]
     - [if [eq current-top [i32.const 0]] [unreachable]]
     - [if [not-is-address-aligned current-top] [unreachable]]
     mem-size-bytes [bitwise-shift-left [memory.size 0] [i32.const 16]]
     - [if [gt current-top mem-size-bytes] [unreachable]]
     new-top [add current-top size]]
    [if [gt new-top mem-size-bytes] [unreachable]]
    [s32 [i32.const 0] new-top]
    current-top]]

[defn is-power-of-2 [x] [eq [const 0] [bitwise-and x [dec x]]]]

[defn align [n x]
  [assert n must not be zero]
  [assert [is-power-of-2 n] must be power of 2]
  [let [dn [dec n]]
    [list [quote bitwise-and]
      [list [quote add] [mk-i32 dn] x]
      [mk-i32 [bitwise-xor [i32.const -1] dn]]]]]

[defmacro mem-align [x] [align mem-alloc-size x]]

[import env log-size-pointer [func [i32 i32 i32] []]]

[defn test-log []
  [let [start [i32.const 32]
        end [i32.const 127]
        s [sub end start]
        alloc-size [mem-align s]
        p [bump-alloc alloc-size]]
    [loop [i [i32.const 0]]
      [when [lt i s]
        [store8 0 0 0 [add p i] [add start i]]
        [continue i [add i [i32.const 1]]]]]
    [log-size-pointer 0 s p]
    [bump-free p alloc-size]]]

[defn is-whitespace [c]
  [or [eq c [quote 32]] [eq c [quote 10]]]]

[defn is-between-inclusive [lower c upper]
  [and [le lower c] [le c upper]]]

[defn is-a-to-z [c]
  [is-between-inclusive [quote 97] c [quote 122]]]

[defn is-digit-and-some [c]
  [comment - . / 0 1 2 3 4 5 6 7 8 9]
  [is-between-inclusive [quote 45] c [quote 57]]]

[defn is-word-char [c]
  [or
    [is-a-to-z c]
    [is-digit-and-some c]]]

[defn is-square-bracket [c]
  [or [eq c [quote 91]] [eq c [quote 93]]]]

[defn is-illegal [c]
  [not [or
    [is-word-char c]
    [is-whitespace c]
    [is-square-bracket c]]]]

[defn scan-word-char [start end]
  [loop [j start]
    [if [and [lt j end] [is-word-char [load8-u 0 0 0 j]]]
      [continue j [inc j]]
      j]]]

[defn scan-whitespace [start end]
  [loop [j start]
    [if [and [lt j end] [is-whitespace [load8-u 0 0 0 j]]]
      [continue j [inc j]]
      j]]]

[defn scan-illegal [start end]
  [loop [j start]
    [if [and [lt j end] [is-illegal [load8-u 0 0 0 j]]]
      [continue j [inc j]]
      j]]]

[defn create-size-buffer [capacity]
  [assert [not [is-negative capacity]] capacity must not be negative]
  [let [p [bump-alloc [add 4 capacity]]]
    [s32 p capacity]
    p]]

[comment a i32 size and a pointer to a buffer that has buffer size and ]
[defn create-growable-list [initial-capacity]
  [let [p [bump-alloc [const 8]]]
    [s32 p [i32.const 0]]
    [s32-offset 4 p [create-size-buffer initial-capacity]]
    p]]

[defn get-size [list]
  [let [s [l32 list]]
    [when [is-negative s] [unreachable]]
    s]]

[defn get-capacity [list] [l32 [l32-offset 4 list]]]

[defn get-buffer-pointer [list] [add 4 [l32-offset 4 list]]]

[defn push-n-bytes [list-p n-bytes]
  [assert [not [is-negative n-bytes]] size must not be negative]
  [let [size [get-size list-p]
        new-size [add size n-bytes]
        buffer [l32-offset 4 list-p]
        buffer-capacity [l32 buffer]]
    [when [is-negative buffer-capacity] [unreachable]]
    [when [lt buffer-capacity n-bytes]
      [unreachable]]
    [s32 list-p new-size]
    [when [gt new-size buffer-capacity]
      [let [new-buffer [create-size-buffer [add buffer-capacity buffer-capacity]]]
        [memory.copy 0 [add new-buffer 4] [add buffer 4] size]
        [try-bump-free buffer [add 4 buffer-capacity]]
        [s32-offset 4 list-p new-buffer]]]]]

[defn pop-n-bytes [list n-bytes]
  [assert [not [is-negative n-bytes]] size must not be negative]
  [let [size [get-size list]
        new-size [sub size n-bytes]
        buffer [l32-offset 4 list]]
    [assert [not [is-negative new-size]] cannot pop more than what is in the list]
    [s32 list new-size]]]

[defn push-i32 [l i]
  [let [size [get-size l]
        -  [push-n-bytes l 4]
        buffer [l32-offset 4 l]]
    [s32-offset 4 [add buffer size] i]]]

[defn push-i8 [l i]
  [let [size [get-size l]
        - [push-n-bytes l 1]
        buffer [l32-offset 4 l]]
    [store8 0 0 4 [add buffer size] i]]]

[defn at-i32 [list i]
  [when [is-negative i] [unreachable]]
  [when [ge i [div [get-size list] 4]] [unreachable]]
  [l32-offset 4 [add [l32-offset 4 list] [mul i 4]]]]

[defmacro l16 [addr] [list [quote i32.load16-u] 0 1 0 addr]]

[defn lex-one-utf16 [p end]
  [when [ge p end] [unreachable]]
  [let [c [l16 p]]
    [ifs
      [is-word-char c]
      [loop [j [add 2 p]]
        [if [and [lt j end] [is-word-char [l16 j]]]
          [continue j [add 2 j]]
          [list [quote word] [sub j p]]]]

      [is-whitespace c]
      [loop [j [add 2 p]]
        [if [and [lt j end] [is-whitespace [l16 j]]]
          [continue j [add 2 j]]
          [list [quote wspc] [sub j p]]]]

      [eq c [const 91]]
      [list [quote lsqb] 2]

      [eq c [const 93]]
      [list [quote rsqb] 2]

      [loop [j [add 2 p]]
        [if [and [lt j end] [is-illegal [l16 j]]]
          [continue j [add 2 j]]
          [list [quote ille] [sub j p]]]]]]]

[defn create-terminal [tag size]
  [let [p [bump-alloc [const 8]]]
    [s32 p size]
    [s32-offset 4 p tag]
    p]]

[def tag-node-list [quote 0x6c697374]]
[def tag-node-root [quote 0x726f6f74]]
[def tag-node-word [const 0x776f7264]]

[defn set-child-n [non-term n child]
  [s32-offset 8 [add non-term [mul n 4]] child]]

[defn get-node-size [node] [l32 node]]

[defn get-node-tag [node] [l32-offset 4 node]]

[defn is-non-terminal [form]
  [let [tag [get-node-tag form]]
    [or [eq tag-node-list tag] [eq tag-node-root tag]]]]

[defn get-node-number-of-children [node]
  [if [is-non-terminal node]
    [l32-offset 8 node]
    0]]

[defn get-node-child [node i]
  [assert [not [is-negative i]] get-node-child i must not be negative]
  [let [n [get-node-number-of-children node]]
    [assert [lt i n] get-node-child i must be less than number of children]
    [l32-offset 12 [add node [mul i 4]]]]]

[defn sum-byte-sizes [nodes]
  [let [n [div [get-size nodes] 4]]
    [loop [i 0 sum 0]
      [if [lt i n]
        [let [child [at-i32 nodes i]]
          [continue
            i [inc i]
            sum [add sum [get-node-size child]]]]
        sum]]]]

[defn create-non-terminal-from-list [tag children]
  [let [s [get-size children]
        p [bump-alloc [add [const 12] s]]]
    [s32 p [sum-byte-sizes children]]
    [s32-offset 4 p tag]
    [s32-offset 8 p [div s 4]]
    [memory.copy 0 [add p [const 12]] [add 4 [l32-offset 4 children]] s]
    p]]

[defn last-i32 [list]
  [let [s [get-size list]]
    [assert [gt s 0] last-i32 list must not be empty]
    [at-i32 list [dec [div s 4]]]]]

[comment
  wspc 0x77737063
  ille 0x696c6c65
  lsqb 0x6c737162
  rsqb 0x72737162
  ]

[defn parse [start end]
  [let [root-children [create-growable-list [const 4]]
        stack [create-growable-list [const 4]]]
    [push-i32 stack root-children]
    [loop [p start]
      [when [lt p end]
        [let [c [l16 p]]
          [ifs
            [is-word-char c]
            [let [wend [loop [q [add 2 p]] [if [and [lt q end] [is-word-char [l16 q]]] [continue q [add 2 q]] q]]]
              [push-i32
                [last-i32 stack]
                [create-terminal tag-node-word [sub wend p]]]
              [continue p wend]]

            [is-whitespace c]
            [let [wend [loop [q [add 2 p]] [if [and [lt q end] [is-whitespace [l16 q]]] [continue q [add 2 q]] q]]]
              [push-i32
                [last-i32 stack]
                [create-terminal [const 0x77737063] [sub wend p]]]
              [continue p wend]]

            [eq c [const 91]]
            [let [new-children-list [create-growable-list [const 8]]]
              [comment [if [there is already an empty list on the stack reuse it] [create-growable-list [const 8]]]]
              [push-i32 new-children-list [create-terminal [const 0x6c737162] 2]]
              [push-i32 stack new-children-list]
              [continue p [add 2 p]]]

            [eq c [const 93]]
            [let [stack-size [div [get-size stack] 4]
                  top [at-i32 stack [dec stack-size]]]
              [push-i32 top [create-terminal [const 0x72737162] 2]]
              [when [lt 1 stack-size]
                [let [children [last-i32 stack]]
                  [pop-n-bytes stack 4]
                  [push-i32 [last-i32 stack] [create-non-terminal-from-list tag-node-list children]]]]
              [continue p [add 2 p]]]

            [let [wend [loop [q [add 2 p]] [if [and [lt q end] [is-illegal [l16 q]]] [continue q [add 2 q]] q]]]
              [push-i32
                [last-i32 stack]
                [create-terminal [const 0x696c6c65] [sub wend p]]]
              [continue p wend]]]]]]
    [loop [stack-size [div [get-size stack] 4]]
      [when [lt 1 stack-size]
        [let [children [at-i32 stack [dec stack-size]]]
          [pop-n-bytes stack 4]
          [push-i32 [last-i32 stack] [create-non-terminal-from-list tag-node-list children]]]
        [continue stack-size [dec stack-size]]]]
    [create-non-terminal-from-list tag-node-root root-children]]]

[def tag-word [const 97]]
[def tag-list [const 5]]

[defn alloc-tagged-word [n-bytes]
  [let [p [bump-alloc [add [const 8] [mem-align n-bytes]]]]
    [s32 p tag-word]
    [s32-offset 4 p n-bytes]
    p]]

[defn is-word-pointer [form]
  [eq [l32 form] tag-word]]

[defn word-size [word]
  [l32-offset 4 word]]

[defn word-pointer [word]
  [add word [const 8]]]

[defn validate-word [word]
  [let [s [l32-offset 4 word]]
    [when [is-negative s] [unreachable]]
    [when [eq 0 s] [unreachable]]
    [when [not [eq [l32 word] tag-word]] [unreachable]]
    [loop [i 0]
      [when [lt i s]
        [when [not [is-word-char [load8-u 0 0 8 i]]]
          [unreachable]]
        [continue i [inc i]]]]
    s]]

[defn alloc-tagged-list [n-elements]
  [let [p [bump-alloc [add [const 8] [mul 4 n-elements]]]]
    [s32 p tag-list]
    [s32-offset 4 p n-elements]
    p]]

[defn is-list-pointer [form]
  [eq [l32 form] tag-list]]

[defn tag [form] [l32 form]]

[defn list-size [list]
  [l32-offset 4 list]]

[defn at-alloc-list [form i]
  [assert [is-list-pointer form] at-alloc-list1 form must be a list]
  [assert [not [is-negative i]] at-alloc-list2 i must not be negative]
  [let [s [list-size form]]
    [assert [lt i s] at-alloc-list3 i must be less than the size of the list]
    [l32-offset 8 [add form [mul i 4]]]]]

[defn number-of-word-or-list [node]
  [let [n-children [get-node-number-of-children node]]
    [loop [i 0 sum 0]
      [if [ge i n-children]
        sum
        [let [child [get-node-child node i]
              tag [get-node-tag child]]
          [if [or [eq tag tag-node-word] [eq tag tag-node-list]]
            [continue i [inc i] sum [inc sum]]
            [continue i [inc i]]]]]]]]

[defn node-to-form [text node]
  [let [tag [get-node-tag node]]
    [ifs
      [eq tag tag-node-word]
      [let [ns [get-node-size node]
            s [div ns 2]
            end [add text ns]
            tw [alloc-tagged-word s]]
        [loop [cur text wp [word-pointer tw]]
          [when [lt cur end]
            [store8 0 0 0 wp [load16-u 0 1 0 cur]]
            [continue cur [add 2 cur] wp [inc wp]]]]
        tw]

      [eq tag tag-node-list]
      [let [n-form-children [number-of-word-or-list node]
            tl [alloc-tagged-list n-form-children]
            n-children [get-node-number-of-children node]]
        [loop [p text node-index 0 form-index 0]
          [if [ge node-index n-children]
            tl
            [let [child [get-node-child node node-index]
                  s [get-node-size child]
                  form [node-to-form p child]]
              [if form
                [do
                  [s32-offset 8 [add tl [mul form-index 4]] form]
                  [continue p [add p s] node-index [inc node-index] form-index [inc form-index]]]
                [continue p [add p s] node-index [inc node-index]]]]]]]

       0]]]

[defn tree-to-forms [text node]
  [let [n-form-children [number-of-word-or-list node]
        gl [create-growable-list [mul 4 n-form-children]]
        n-children [get-node-number-of-children node]]
    [loop [p text node-index 0]
      [if [ge node-index n-children]
        gl
        [let [child [get-node-child node node-index]
              s [get-node-size child]
              form [node-to-form p child]]
          [when form [push-i32 gl form]]
          [continue p [add p s] node-index [inc node-index]]]]]]]

[defn print-recursive [mut-list form]
  [ifs
    [is-word-pointer form]
    [let [s [word-size form]
          size-before [get-size mut-list]]
      [push-n-bytes mut-list s]
      [loop [p [add size-before [l32-offset 4 mut-list]] i 0]
        [when [lt i s]
          [store8 0 0 4 p [load8-u 0 0 8 [add form i]]]
          [continue p [inc p] i [inc i]]]]]

    [is-list-pointer form]
    [let [s [list-size form]]
      [push-i8 mut-list [const 91]]
      [when s
        [print-recursive mut-list [at-alloc-list form 0]]
        [for i 1 s
          [push-i8 mut-list [const 32]]
          [print-recursive mut-list [at-alloc-list form i]]]]
      [push-i8 mut-list [const 93]]]

    [do [log [list [quote unknown node] form [l32 form]]]
      [unreachable]]]]

[defn print [node]
  [let [mut-list [create-growable-list [const 64]]]
    [print-recursive mut-list node]
    mut-list]]

[defn compile [form]
  [ifs
    [is-word-pointer form]
    []

    [is-list-pointer form]
    [let [s []]
      ]

    [do [log [quote unknown form]]
      [unreachable]]]]

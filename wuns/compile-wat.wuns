[load check2.wuns]

[type wat-type-basic []
  [union
    [i32]
    [i64]
    [f32]
    [f64]]]

[type wat-inst []
  [union
    [i32.const word]
    [f64.const word]
    [local.get word]
    [local.set word wat-inst]
    [inst word [list wat-inst]]
    [if [list wat-type-basic] wat-inst wat-inst wat-inst]
    [call word [list wat-inst]]
    [instructions [list wat-inst]]
    [loop [list wat-type-basic] wat-inst]
    [br word]]]

[type output-func []
  [record
    [name word]
    [parameters [list [pair word wat-type-basic]]]
    [locals [list [pair word wat-type-basic]]]
    [result [list wat-type-basic]]
    [body wat-inst]]]

[type export-kind []
  [union [func] [global]]]

[type wat-output []
  [record
    [funcs [growable-list output-func]]
    [exports [growable-list [pair word [pair export-kind word]]]]]]

[defn make-wat-output []
  [wat-output [growable-list] [growable-list]]]

[type translate-context []
  [record
    [output wat-output]
    [check-context check-context]]]

[defn translate-type [it]
  [match [normalize-type it]
    [ctype/inst inst-type]
    [match inst-type
      [inst-type/apply type-name type-args]
      [switch type-name
        [[word i32]] [wat-type-basic/i32]
        [[word f64]] [wat-type-basic/f64]
        [do [log it] [logq-abort not implemented translate-type inst-type/apply]]]

      [do [log it] [logq-abort not implemented translate-type inst-type/apply]]]
    [do [log it] [logq-abort not implemented translate-type inst-type/apply]]]]

[defn translate-types [it]
  [match [normalize-type it]
    [ctype/inst inst-type]
    [match inst-type
      [inst-type/apply type-name type-args]
      [switch type-name
        [[word i32]] [list [wat-type-basic/i32]]
        [[word f64]] [list [wat-type-basic/f64]]
        [[word tuple]] [list-map ta type-args [translate-type ta]]

        [do [log it] [logq-abort not implemented translate-types inst-type/apply]]]
      [do [log it] [logq-abort not implemented translate-types inst-type/apply]]]
    [do [log it] [logq-abort not implemented translate-types inst-type/apply]]]]

[defn get-annotation [tctx bst]
  [get [check-context/type-annotations [translate-context/check-context tctx]] [to-js-value bst]]]

[defn get-form-types [tctx bform]
  [match bform
    [bexp/literal l]
    [match l
      [literal/i32 i] [list [wat-type-basic/i32]]
      [literal/f64 f] [list [wat-type-basic/f64]]
      [literal/word w] [logq-abort not implemented get-form-types literal/word]]

    [bexp/do forms]
    [if [is-empty forms]
      [list]
      [get-form-types tctx [last forms]]]

    [translate-types
      [get-annotation tctx bform]]
    ]]

[comment
      [[word i32.div-s]] [bin-0 [binop/div]]
      [[word i32.rem-s]] [bin-0 [binop/rem]]
      [[word i32.lt-s]]  [bin-0 [binop/lt]]
      [[word i32.le-s]]  [bin-0 [binop/le]]
      [[word i32.gt-s]]  [bin-0 [binop/gt]]
      [[word i32.ge-s]]  [bin-0 [binop/ge]]
 ]

[defn instruction-name-and-args-to-inst [inst-name cargs]
  [assert [eq [size cargs] 2] instruction-name-and-args-to-inst expects 2 arguments]
  [switch inst-name
    [[word i32.add] [word i32.sub] [word i32.mul] [word i32.eq] [word i32.ne]
      [word i32.and] [word i32.or]
      [word f64.add] [word f64.sub] [word f64.mul]] [wat-inst/inst inst-name cargs]

    [do
      [log inst-name]
      [logq-abort instruction-name-and-args-to-inst not implemented]]]]

[defn mk-translate-form [tctx]
  [func go [form]
    [match form
      [bexp/var w]
      [wat-inst/local.get [syntax-word/word w]]

      [bexp/literal l]
      [match l
        [literal/i32 i] [wat-inst/i32.const [syntax-word/word i]]
        [literal/f64 f] [wat-inst/f64.const [syntax-word/word f]]
        [literal/word w] [logq-abort not implemented translate-form literal/word]]

      [bexp/do forms]
      [if [is-empty forms]
        [wat-inst/instructions [list]]
        [wat-inst/instructions
          [concat
            [list-map form [slice forms 0 [dec [size forms]]]
              [let [types [get-form-types tctx form]]
                [todo replace with  [loop [res [go form] i [size types]]
                  [if i
                    [continue
                      res [wat-inst/inst [word drop] [list res]]
                      i [dec i]]
                    res]] ]
                [[func loopi [i]
                  [if i
                    [wat-inst/inst [word drop] [list [loopi [dec i]]]]
                    [go form]]]
                  [size types]]]]
            [list [go [last forms]]]]]]

      [bexp/if cond true false]
      [wat-inst/if
        [get-form-types tctx form]
        [go cond]
        [go true]
        [go false]]

      [bexp/call f args]
      [match f
        [bexp/intrinsic i]
        [instruction-name-and-args-to-inst [syntax-word/word i]
          [list-map arg args [go arg]]]

        [bexp/var w]
        [wat-inst/call [syntax-word/word w] [list-map a args [go a]]]

        [logq-abort translate-form call not implemented]]

      [bexp/loop bindings body]
      [do
        [assert [eq [size body] 1] expected one body]
        [wat-inst/instructions
          [concat
            [list-map b bindings
              [wat-inst/local.set [syntax-word/word [pair/fst b]] [go [pair/snd b]]]]
            [list
              [wat-inst/loop
                [get-form-types tctx form]
                [wat-inst/instructions
                  [list-map b body [go b]]]]]]]]

      [bexp/continue assignments]
      [wat-inst/instructions
        [concat
          [list-map a assignments [wat-inst/local.set [syntax-word/word [pair/fst a]] [go [pair/snd a]]]]
          [list [wat-inst/br [word 1]]]]]

      [do
        [log form]
        [logq-abort not implemented translate-form]]]]]

[defn get-func-type [t]
  [match [normalize-type t]
    [ctype/inst inst-type]
    [match inst-type
      [inst-type/func func-type]
      func-type

      [logq-abort get-func-type expected a func type]]

    [logq-abort get-func-type expected a func type]]]

[defn translate-top-form [tctx bform]
  [match bform
    [btop/defunc kind f]
    [let [t [get [check-context/type-annotations [translate-context/check-context tctx]] [to-js-value f]]
          ft [get-func-type t]
          param-types [func-type/params ft]
          params [bfunc/parameters f]
          bodies [bfunc/body f]
          gparams [growable-list]
          cctx [translate-context/check-context tctx]
          glocals [growable-list]]
      [assert [eq [size params] [size param-types]] expected matching number of parameters and types]
      [for i 0 [size params]
        [push gparams
          [pair [syntax-word/word [at params i]] [translate-type [at param-types i]]]]]
      [let [proc
            [bexp-recur
              [func f [bform]
                [match bform
                  [bexp/let bs body]
                  [for-each b bs
                    [push glocals [pair [syntax-word/word [pair/fst b]] [translate-type [get-annotation tctx [pair/snd b]]]]]]

                  [bexp/loop bs body]
                  [for-each b bs
                    [push glocals [pair [syntax-word/word [pair/fst b]] [translate-type [get-annotation tctx [pair/snd b]]]]]]

                  [do]]
                true]]
            tform [mk-translate-form tctx]
            wat-bodies
            [list-map b bodies
              [do
                [proc b]
                [tform b]]]]
      [push
        [wat-output/funcs [translate-context/output tctx]]
        [output-func
          [syntax-word/word [bfunc/name f]]
          [clone-growable-to-frozen-list gparams]
          [clone-growable-to-frozen-list glocals]
          [translate-types [func-type/result ft]]
          [wat-inst/instructions wat-bodies]]]]]

    [btop/export snames]
    [for-each sname snames
      [let [export-kind
            [match [normalize-type [get-annotation tctx sname]]
              [ctype/inst inst-type]
              [match inst-type
                [inst-type/func func-type]
                [export-kind/func]

                [export-kind/global]]

              [logq-abort get-func-type expected a func type]]]
      [push
        [wat-output/exports [translate-context/output tctx]]
        [pair [syntax-word/word sname] [pair export-kind [syntax-word/word sname]]]]]]

    [do
      [log bform]
      [logq-abort not implemented translate-top-form bform]]]]

[defn translate-top-forms [forms]
  [let [converter [mk-form-to-ast [word dummy-dir-name]]
        check-ctx [make-global-context-from-syntax-info [form-to-ast-converter/syntax-info converter]]
        f2t [form-to-ast-converter/form-to-top converter]
        check-top [checker/check-top [make-checker check-ctx]]
        output [make-wat-output]
        tctx [translate-context output check-ctx]]
    [for-each form forms
      [let [btop [f2t form]]
        [check-top btop]
        [let [check-errors [clone-growable-to-frozen-list [check-context/messages check-ctx]]]
          [when-not [is-empty check-errors]
            [for-each error check-errors [log-check-message error]]
            [logq-abort translate-top-forms binding errors]]]
        [translate-top-form tctx btop]]]
    output]]

[def ascii-line-feed [i32 10]]
[def ascii-space [i32 32]]
[def ascii-double-quote [i32 34]]
[def ascii-dollar [i32 36]]
[def ascii-left-paren [i32 40]]
[def ascii-right-paren [i32 41]]
[def ascii-equal [i32 61]]
[def ascii-backslash [i32 92]]

[type printer []
  [record
    [bytes [atom byte-array]]
    [index [atom i32]]]]

[defn empty-printer [init-capacity]
  [printer [atom [byte-array init-capacity]] [atom 0]]]

[defn printer-to-byte-array [p]
  [let
    [ci [atom-get [printer/index p]]
     cb [atom-get [printer/bytes p]]
     trimmed [byte-array ci]]
    [for i 0 ci
      [byte-array-set trimmed i [byte-array-get cb i]]]
    trimmed]]

[defn print-byte [printer byte]
  [let [ia [printer/index printer]
        i [atom-get ia]
        ba [printer/bytes printer]
        a [atom-get ba]]
    [assert [lt-s i [byte-array-size a]] printer buffer overflow]
    [byte-array-set a i byte]
    [atom-set [printer/index printer] [inc i]]]]

[defn print-word [printer word]
  [for i 0 [word-byte-size word]
    [print-byte printer [char-code-at word i]]]]

[type wat []
  [union
    [word word]
    [dollar word]
    [dqstring word]
    [seq [list wat]]
    [list [list wat]]]]

[defn print-wat [printer wat]
  [match wat
    [wat/word w] [print-word printer w]
    [wat/dollar w] [do [print-byte printer ascii-dollar] [print-word printer w]]
    [wat/dqstring w] [do [print-byte printer ascii-double-quote] [print-word printer w] [print-byte printer ascii-double-quote]]
    [wat/seq l]
      [do
        [when-not [is-empty l]
          [print-wat printer [first l]]
          [for i 1 [size l]
            [print-byte printer ascii-space]
            [print-wat printer [at l i]]]]]
    [wat/list l]
      [do
        [print-byte printer ascii-left-paren]
        [when-not [is-empty l]
          [print-wat printer [first l]]
          [for i 1 [size l]
            [print-byte printer ascii-space]
            [print-wat printer [at l i]]]]
        [print-byte printer ascii-right-paren]]
      ]]

[defn wat-list [.. l] [wat/list l]]

[defn wat-type-basic-to-word [t]
  [match t
    [wat-type-basic/i32] [word i32]
    [wat-type-basic/f64] [word f64]]]

[defn wat-concat [.. l] [wat/list [concat-lists l]]]

[defn wat-inst-to-out [i]
  [match i
    [wat-inst/i32.const w]
    [wat-concat
      [list [wat/word [word i32.const]] [wat/word w]]]

    [wat-inst/f64.const w]
    [wat-concat
      [list [wat/word [word f64.const]] [wat/word w]]]

    [wat-inst/inst w args]
    [wat-concat
      [list [wat/word w]] [list-map a args [wat-inst-to-out a]]]

    [wat-inst/local.get w]
    [wat-list [wat/word [word local.get]] [wat/dollar w]]

    [wat-inst/local.set w e]
    [wat-list [wat/word [word local.set]] [wat/dollar w] [wat-inst-to-out e]]

    [wat-inst/instructions insts]
    [wat/seq [list-map inst insts [wat-inst-to-out inst]]]

    [wat-inst/if result-types cond true false]
    [wat-list [wat/word [word if]]
      [wat/list
        [concat [list [wat/word [word result]]]
          [list-map rt result-types [wat/word [wat-type-basic-to-word rt]]]]]
      [wat-inst-to-out cond]
      [wat-list [wat/word [word then]] [wat-inst-to-out true]]
      [wat-list [wat/word [word else]] [wat-inst-to-out false]]]

    [wat-inst/call w args]
    [wat-concat
      [list [wat/word [word call]] [wat/dollar w]]
      [list-map a args [wat-inst-to-out a]]]

    [wat-inst/loop result-types body]
    [wat-list
      [wat/word [word loop]]
      [wat/list
        [concat [list [wat/word [word result]]]
          [list-map rt result-types [wat/word [wat-type-basic-to-word rt]]]]]
      [wat-inst-to-out body]]

    [wat-inst/br w]
    [wat-list [wat/word [word br]] [wat/word w]]

    [logq-abort wat-inst-to-out unknown instruction]]]

[defn output-func-to-wat [of]
  [wat-concat
    [list
      [wat/word [word func]]
      [wat/dollar [output-func/name of]]]
    [list-map p [output-func/parameters of]
      [wat-list [wat/word [word param]] [wat/dollar [pair/fst p]] [wat/word [wat-type-basic-to-word [pair/snd p]]]]]
    [list
      [wat-concat
        [list [wat/word [word result]]]
        [list-map rt [output-func/result of] [wat/word [wat-type-basic-to-word rt]]]]]
    [list-map l [output-func/locals of]
      [wat-list [wat/word [word local]] [wat/dollar [pair/fst l]] [wat/word [wat-type-basic-to-word [pair/snd l]]]]]
    [list
      [wat-inst-to-out [output-func/body of]]]]]

[defn export-to-wat [p]
  [wat-concat
    [list
      [wat/word [word export]]
      [wat/dqstring [pair/fst p]]
      [let [kind-pair [pair/snd p]]
        [wat-list
          [wat/word
            [match [pair/fst kind-pair]
              [export-kind/func] [word func]
              [logq-abort export-to-wat unknown export kind]]]
          [wat/dollar [pair/snd kind-pair]]]]]]]

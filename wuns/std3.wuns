[def -2 [i32.const -2]]
[def -1 [i32.const -1]]
[def 0 [i32.const 0]]
[def 1 [i32.const 1]]
[def 2 [i32.const 2]]
[def 3 [i32.const 3]]
[def 4 [i32.const 4]]

[def quote-word [quote quote]]

[defn list [.. entries] entries]

[defn mk-quote [form] [list quote-word form]]

[defmacro comment [.. -] [list]]

[defn inc [x] [add x 1]]

[defn dec [x] [sub x 1]]

[defn not [c] [if c 0 1]]

[defn is-zero [x] [eq x 0]]

[defn is-odd [x] [bitwise-and x 1]]

[defn is-even [x] [not [is-odd x]]]

[defn is-negative [x] [lt-s x 0]]

[defn first [l] [at l 0]]
[defn second [l] [at l 1]]
[defn third [l] [at l 2]]
[defn fourth [l] [at l 3]]
[defn last [l] [at l -1]]

[defn concat [.. lists]
  [let
    [n-of-lists [size lists]
     total-size
      [loop [total 0 i 0]
        [if [lt-s i n-of-lists]
          [continue total [add total [size [at lists i]]] i [inc i]]
          total]]
     result [mutable-list-of-size total-size]]
    [loop [i 0 j 0 res-index 0]
      [if [lt-s i n-of-lists]
        [let [cur-list [at lists i]
              s [size cur-list]]
          [loop [i 0 res-index res-index]
            [if [lt-s i s]
              [let []
                [set-array result res-index [at cur-list i]]
                [continue res-index [inc res-index] i [inc i]]]]]
          [continue i [inc i] res-index [add s res-index]]]
        [persistent-array result]]]]]

[defmacro do [.. forms]
  [concat [quote let []] forms]]

[defmacro when [cond .. forms]
  [list
    [quote if] cond
    [concat [quote let []] forms]]]

[defmacro when-not [cond .. forms]
  [list
    [quote if] cond
    [list]
    [concat [quote let []] forms]]]

[defmacro if-not [cond false-form true-form]
  [list
    [quote if] cond
    true-form
    false-form]]

[defn quote-list [lmsg]
  [let [s [size lmsg]
        msg [mutable-list-of-size [inc s]]]
    [set-array msg 0 [quote list]]
    [loop [i 0]
      [when [lt-s i s]
        [set-array msg [inc i] [let [e [at lmsg i]] [if [is-word e] [mk-quote e] e]]]
        [continue i [inc i]]]]
    [persistent-array msg]]]

[defmacro assert [cond .. lmsg]
  [list [quote if] cond
    [list]
    [list [quote let] [list]
      [list [quote log] [quote-list lmsg]]
      [quote [unreachable]]]]]

[defmacro logq [.. lmsg]
  [list [quote log] [quote-list lmsg]]]

[defn identity [x] x]

[defmacro ifs [.. clauses]
  [let [s [size clauses]]
    [assert [is-odd s] ifs requires an odd number of arguments]
    [loop [i [dec s] res [at clauses -1]]
      [if [eq i 0]
        res
        [continue
          i [sub i 2]
          res [list [quote if] [at clauses [sub i 2]] [at clauses [dec i]] res]]]]]]

[defmacro and [.. clauses]
  [let [s [size clauses]]
    [if [is-zero s]
      1
      [loop [i [dec s]
             res [at clauses -1]]
        [if [eq i 0]
          res
          [continue
            i [dec i]
            res [list [quote if] [at clauses [dec i]] res 0]]]]]]]

[def word-counter [atom 0]]

[defn genword []
  [let [c [atom-get word-counter]]
    [atom-set word-counter [inc c]]
    [concat-words [quote genword] [int-to-word c]]]]

[defmacro or [.. clauses]
  [let [s [size clauses]]
    [if [is-zero s]
      0
      [loop [i [dec s]
             res [at clauses -1]]
        [if [eq i 0]
          res
          [continue
            i [dec i]
            res
            [let [w [genword]]
              [list [quote let] [list w [at clauses [dec i]]]
                [list [quote if] w w res]]]]]]]]]

[defmacro if-let [binding true-form false-form]
  [assert [is-list binding] if-let requires a list as the first argument]
  [assert [eq 2 [size binding]] if-let requires a list of size 2 as the first argument]
  [let [v [first binding]
        cond [second binding]
        tmp-word [genword]]
    [assert [is-word v] if-let requires a word as the first element of the first argument]
    [list [quote let] [list tmp-word cond]
      [list [quote if] tmp-word [list [quote let] [list v tmp-word] true-form] false-form]]]]

[defn eq-form [a b]
  [or
    [eq-word a b]
    [and [is-list a] [is-list b]
      [let [sa [size a]]
        [and [eq sa [size b]]
          [loop [i 0]
            [if [lt-s i sa]
              [if [recur [at a i] [at b i]]
                [continue i [inc i]]
                0]
              1]]]]]]]

[defn for-func [iw start increment end forms]
  [assert [lt-s 0 increment] for-func increment must be positive]
  [let [ew [genword]]
    [list
      [quote let] [list ew end]
      [list
        [quote loop] [list iw start]
        [list [quote if]
          [list [quote i32.lt-s] iw ew]
          [concat [quote let []] forms
            [list [list [quote continue] iw [list [quote i32.add] iw increment]]]]
          [list]]]]]]

[defmacro for [iw start end .. forms]
  [for-func iw start [i32.const 1] end forms]]

[defmacro for-pair [iw start end .. forms]
  [for-func iw start [i32.const 2] end forms]]

[defmacro for-each [element-var collection .. forms]
  [assert [is-word element-var] for-each requires a word as the first argument]
  [let [iteration-var [genword]
        col-var [genword]
        col-size-var [genword]]
    [list
      [quote let] [list col-var collection col-size-var [list [quote size] col-var]]
      [list
        [quote loop] [list iteration-var 0]
        [list [quote if]
          [list [quote i32.lt-s] iteration-var col-size-var]
          [concat [quote [let]]
            [list [list element-var [list [quote at] col-var iteration-var]]]
            forms
            [list [list [quote continue] iteration-var [list [quote i32.add] iteration-var 1]]]]]]]]]

[defmacro kv-map [.. entries]
  [let [s [size entries]
        ml [mutable-list-of-size [add 3 s]]
        tmp-word [genword]]
    [assert [is-even s] kv-map requires an even number of arguments]
    [set-array ml 0 [quote let]]
    [set-array ml 1 [list tmp-word [quote [transient-kv-map]]]]
    [for-pair i 0 s
      [set-array ml [add 2 i]
        [let [k [at entries i]]
          [list [quote set] tmp-word [if [is-word k] [mk-quote k] k] [at entries [inc i]]]]]]
    [set-array ml [add 2 s] [list [quote persistent-kv-map] tmp-word]]
    ml]]

[defmacro getq [m w]
  [list [quote get] m [mk-quote w]]]

[defmacro hasq [m w]
  [list [quote has] m [mk-quote w]]]

[defmacro setq [m w v]
  [list [quote set] m [mk-quote w] v]]

[defn is-empty [form]
  [eq 0 [size form]]]

[defn push-all [mut-list list]
  [for-each e list [push mut-list e]]]

[defn assoc [m k v]
  [let [new [transient-kv-map]]
    [for-each k [keys m]
      [set new k [get m k]]]
    [set new k v]
    [persistent-kv-map new]]]

[defn assoc-meta [f k v]
  [with-meta f [assoc [meta f] k v]]]

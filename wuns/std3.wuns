[def -2 [i32.const -2]]
[def -1 [i32.const -1]]
[def 0 [i32.const 0]]
[def 1 [i32.const 1]]
[def 2 [i32.const 2]]
[def 3 [i32.const 3]]
[def 4 [i32.const 4]]

[def quote-word [quote quote]]

[defn mk-quote [form] [list quote-word form]]

[defmacro comment [.. -] [list]]

[defn inc [x] [add x 1]]

[defn dec [x] [sub x 1]]

[defn not [c] [if c 0 1]]

[defn is-zero [x] [eq x 0]]

[defn is-odd [x] [bitwise-and x 1]]

[defn is-even [x] [not [is-odd x]]]

[defn is-negative [x] [lt-s x 0]]

[defn first [l] [at l 0]]
[defn second [l] [at l 1]]
[defn third [l] [at l 2]]
[defn fourth [l] [at l 3]]
[defn last [l] [at l -1]]

[defmacro do [.. forms]
  [concat [quote let []] forms]]

[defmacro when [cond .. forms]
  [list
    [quote if] cond
    [concat [quote let []] forms]]]

[defmacro when-not [cond .. forms]
  [list
    [quote if] cond
    [list]
    [concat [quote let []] forms]]]

[defmacro if-not [cond false-form true-form]
  [list
    [quote if] cond
    true-form
    false-form]]

[defn quote-list [lmsg]
  [let [s [size lmsg]
        msg [mutable-list-of-size [inc s]]]
    [set-array msg 0 [quote list]]
    [loop [i 0]
      [when [lt-s i s]
        [set-array msg [inc i] [let [e [at lmsg i]] [if [is-word e] [mk-quote e] e]]]
        [continue i [inc i]]]]
    [persistent-array msg]]]

[defmacro assert [cond .. lmsg]
  [list [quote if] cond
    [list]
    [list [quote let] [list]
      [list [quote log] [quote-list lmsg]]
      [quote [unreachable]]]]]

[defmacro logq [.. lmsg]
  [list [quote log] [quote-list lmsg]]]

[defn identity [x] x]

[defmacro ifs [.. clauses]
  [let [s [size clauses]]
    [assert [is-odd s] ifs requires an odd number of arguments]
    [loop [i [dec s] res [at clauses -1]]
      [if [eq i 0]
        res
        [continue
          i [sub i 2]
          res [list [quote if] [at clauses [sub i 2]] [at clauses [dec i]] res]]]]]]

[defmacro and [.. clauses]
  [let [s [size clauses]]
    [if [is-zero s]
      1
      [loop [i [dec s]
             res [at clauses -1]]
        [if [eq i 0]
          res
          [continue
            i [dec i]
            res [list [quote if] [at clauses [dec i]] res 0]]]]]]]

[def word-counter [atom 0]]

[defn genword []
  [let [c [atom-get word-counter]]
    [atom-set word-counter [inc c]]
    [concat-words [list [quote genword] [int-to-word c]]]]]

[defmacro or [.. clauses]
  [let [s [size clauses]]
    [if [is-zero s]
      0
      [loop [i [dec s]
             res [at clauses -1]]
        [if [eq i 0]
          res
          [continue
            i [dec i]
            res
            [let [w [genword]]
              [list [quote let] [list w [at clauses [dec i]]]
                [list [quote if] w w res]]]]]]]]]

[defn eq-form [a b]
  [or
    [eq-word a b]
    [and [is-list a] [is-list b]
      [let [sa [size a]]
        [and [eq sa [size b]]
          [loop [i 0]
            [if [lt-s i sa]
              [if [recur [at a i] [at b i]]
                [continue i [inc i]]
                0]
              1]]]]]]]

[defn for-func [iw start increment end forms]
  [assert [lt-s 0 increment] for-func increment must be positive]
  [let [ew [genword]]
    [list
      [quote let] [list ew end]
      [list
        [quote loop] [list iw start]
        [list [quote if]
          [list [quote i32.lt-s] iw ew]
          [concat [quote let []] forms
            [list [list [quote continue] iw [list [quote i32.add] iw increment]]]]
          [list]]]]]]

[defmacro for [iw start end .. forms]
  [for-func iw start [i32.const 1] end forms]]

[defmacro for-pair [iw start end .. forms]
  [for-func iw start [i32.const 2] end forms]]

[defmacro for-each [element-var collection .. forms]
  [assert [is-word element-var] for-each requires a word as the first argument]
  [let [iteration-var [genword]
        col-var [genword]
        col-size-var [genword]]
    [list
      [quote let] [list col-var collection col-size-var [list [quote size] col-var]]
      [list
        [quote loop] [list iteration-var 0]
        [list [quote if]
          [list [quote i32.lt-s] iteration-var col-size-var]
          [concat [quote [let]]
            [list [list element-var [list [quote at] col-var iteration-var]]]
            forms
            [list [list [quote continue] iteration-var [list [quote i32.add] iteration-var 1]]]]]]]]]

[defn kv-map-push [entries]
  [let [ml [mutable-list]]
    [push ml [quote transient-kv-map]]
    [for-pair i 0 [size entries]
      [push ml
        [let [k [at entries i]]
          [if [is-word k] [mk-quote k] k]]]
      [push ml [at entries [inc i]]]]
    [list [quote persistent-kv-map] ml]]]

[defmacro kv-map [.. entries]
  [let [s [size entries]
        ml [mutable-list-of-size [inc s]]]
    [assert [is-even s] kv-map requires an even number of arguments]
    [set-array ml 0 [quote transient-kv-map]]
    [for-pair i 0 s
      [set-array ml [inc i]
        [let [k [at entries i]]
          [if [is-word k] [mk-quote k] k]]]
      [set-array ml [add 2 i] [at entries [inc i]]]]
    [list [quote persistent-kv-map] ml]]]

[defmacro getq [m w]
  [list [quote get] m [mk-quote w]]]

[defmacro hasq [m w]
  [list [quote has] m [mk-quote w]]]

[defmacro setq [m w v]
  [list [quote set] m [mk-quote w] v]]

[defn is-empty [form]
  [eq 0 [size form]]]

[defn push-all [mut-list list]
  [for-each e list [push mut-list e]]]

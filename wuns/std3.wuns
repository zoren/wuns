[def -2 [quote -2]]
[def -1 [quote -1]]
[def 0 [quote 0]]
[def 1 [quote 1]]
[def 2 [quote 2]]
[def 3 [quote 3]]
[def 4 [quote 4]]

[def quote-word [quote quote]]

[def defmacro
  [func defmacro [name params .. forms]
    [list [quote let] [list]
      [list [quote def] name [concat [list [quote func] name params] forms]]
      [list [quote with-meta] [list [quote var] [list quote-word name]]
        [quote transient-kv-map [quote is-macro] 1]]
      [list]]]]

[let []
  [with-meta [var [quote defmacro]] [transient-kv-map [quote is-macro] 1]]
  [list]]

[defmacro defn [name params .. forms]
  [list [quote def] name [concat [list [quote func] name params] forms]]]

[defn mk-quote [form] [list quote-word form]]

[defmacro comment [.. -] [list]]

[defn inc [x] [add x 1]]

[defn dec [x] [sub x 1]]

[defn not [c] [if c 0 1]]

[defn is-zero [x] [eq x 0]]

[defn is-odd [x] [bitwise-and x 1]]

[defn is-even [x] [not [is-odd x]]]

[defn is-negative [x] [lt x 0]]

[defn first [l] [at l 0]]
[defn second [l] [at l 1]]
[defn third [l] [at l 2]]
[defn fourth [l] [at l 3]]
[defn last [l] [at l -1]]

[defmacro do [.. forms]
  [concat [quote let []] forms]]

[defmacro when [cond .. forms]
  [list
    [quote if] cond
    [concat [quote let []] forms]
    [list]]]

[defmacro when-not [cond .. forms]
  [list
    [quote if] cond
    [list]
    [concat [quote let []] forms]]]

[defmacro if-not [cond false-form true-form]
  [list
    [quote if] cond
    false-form
    true-form]]

[defn quote-list [lmsg]
  [let [msg [mutable-list [quote list]]]
    [loop [i 0]
      [when [lt i [size lmsg]]
        [let [e [at lmsg i]]
          [push msg [if [is-word e] [mk-quote e] e]]
          [continue i [inc i]]]]]
    [persistent-array msg]]]

[defmacro assert [cond .. lmsg]
  [list [quote if] [list [quote not] cond]
    [list [quote let] [list]
      [list [quote log] [quote-list lmsg]]
      [quote [unreachable]]]]]

[defn identity [x] x]

[defmacro ifs [.. clauses]
  [let [s [size clauses]]
    [assert [is-odd s] ifs requires an odd number of arguments]
    [loop [i [dec s] res [at clauses -1]]
      [if [eq i 0]
        res
        [continue
          i [sub i 2]
          res [list [quote if] [at clauses [sub i 2]] [at clauses [dec i]] res]]]]]]

[defmacro and [.. clauses]
  [let [s [size clauses]]
    [if [is-zero s]
      1
      [loop [i [dec s]
             res [at clauses -1]]
        [if [eq i 0]
          res
          [continue
            i [dec i]
            res [list [quote if] [at clauses [dec i]] res 0]]]]]]]

[def word-counter [atom 0]]

[defn genword []
  [let [c [atom-get word-counter]]
    [atom-set word-counter [inc c]]
    [concat-words [list [quote genword] c]]]]

[defmacro or [.. clauses]
  [let [s [size clauses]]
    [if [is-zero s]
      0
      [loop [i [dec s]
             res [at clauses -1]]
        [if [eq i 0]
          res
          [continue
            i [dec i]
            res
            [let [w [genword]]
              [list [quote let] [list w [at clauses [dec i]]]
                [list [quote if] w w res]]]]]]]]]

[defn for-func [iw start increment end forms]
  [assert [gt increment 0] for-func increment must be positive]
  [let [ew [genword]]
    [list
      [quote let] [list ew end]
      [list
        [quote loop] [list iw start]
        [list [quote if]
          [list [quote lt] iw ew]
          [concat [quote let []] forms [list [list [quote continue] iw [list [quote add] iw increment]]]]
          [list]]]]]]

[defmacro for [iw start end .. forms]
  [for-func iw start 1 end forms]]

[defmacro for-inc [iw start increment end .. forms]
  [for-func iw start increment end forms]]

[defmacro for-each [element-var collection .. forms]
  [assert [is-word element-var] for-each requires a word as the first argument]
  [let [iteration-var [genword]
        col-var [genword]
        col-size-var [genword]]
    [list
      [quote let] [list col-var collection col-size-var [list [quote size] col-var]]
      [list
        [quote loop] [list iteration-var 0]
        [list [quote if]
          [list [quote lt] iteration-var col-size-var]
          [concat [quote [let]]
            [list [list element-var [list [quote at] col-var iteration-var]]]
            forms
            [list [list [quote continue] iteration-var [list [quote add] iteration-var 1]]]]
          [list]]]]]]

[defmacro kv-map [.. entries]
  [let [ml [mutable-list]]
    [push ml [quote transient-kv-map]]
    [for-inc i 0 2 [size entries]
      [push ml
        [let [k [at entries i]]
                [if [is-word k]
                  [mk-quote k]
                  k]]]
      [push ml [at entries [inc i]]]]
    [concat [quote [persistent-kv-map]] [list ml]]]]

[defmacro getq [m w]
  [list [quote get] m [mk-quote w]]]

[defmacro hasq [m w]
  [list [quote has] m [mk-quote w]]]

[defmacro setq [m w v]
  [list [quote set] m [mk-quote w] v]]

[defn is-empty [form]
  [eq 0 [size form]]]

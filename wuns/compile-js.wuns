[load ast-bind.wuns]

[type binop []
  [union
    [add]
    [sub]
    [mul]
    [div]
    [rem]

    [eq]
    [ne]

    [lt]
    [le]
    [gt]
    [ge]

    [binary-and]
    [binary-ior]
    ]]

[type js-exp []
  [union
    [number word]
    [string word]
    [var word]
    [array [list js-exp]]
    [object [list [pair word js-exp]]]
    [subscript js-exp js-exp]
    [ternary js-exp js-exp js-exp]
    [call js-exp [list js-exp]]
    [new js-exp [list js-exp]]
    [binop binop js-exp js-exp]
    [arrow-exp [list word] [option word] js-exp]
    [arrow-stmt [list word] [option word] js-stmt]
  ]

  js-stmt []
  [union
    [break]
    [continue]
    [exp js-exp]
    [throw js-exp]
    [return js-exp]
    [const-decl word js-exp]
    [assign word js-exp]
    [if js-exp js-stmt js-stmt]
    [switch js-exp [list [pair [list js-exp] js-stmt]] js-stmt]
    [block [list js-stmt]]
    [seq [list js-stmt]]
    [while js-exp js-stmt]
    [import word word]
    [export [list word]]
  ]
]

[def run-js-stmt
  [type-anno
    [extern js run-js-stmt]
    [func [js-stmt externs-object] [result js-value js-value]]]]

[def write-js-stmt
  [type-anno
    [extern js write-js-stmt]
    [func [word js-stmt] [tuple]]]]

[def call-js-func
  [type-anno
    [extern js call-js-func]
    [func [js-value [list form]] form]]]

[defn block [.. stmts] [js-stmt/block stmts]]

[def js-0 [js-exp/number [word 0]]]
[def js-1 [js-exp/number [word 1]]]

[defn instruction-name-and-args-to-js-exp [inst-name eargs]
  [assert [eq [size eargs] 2] instruction-name-and-args-to-js-exp expects 2 arguments]
  [let [bin [func bin [op] [js-exp/binop op [at eargs 0] [at eargs 1]]]
        bin-0 [func bin-0 [op] [js-exp/binop [binop/binary-ior] [bin op] js-0]]]
    [switch inst-name
      [[word i32.add]]   [bin-0 [binop/add]]
      [[word i32.sub]]   [bin-0 [binop/sub]]
      [[word i32.mul]]   [bin-0 [binop/mul]]
      [[word i32.div-s]] [bin-0 [binop/div]]
      [[word i32.rem-s]] [bin-0 [binop/rem]]

      [[word i32.eq]]    [bin-0 [binop/eq]]
      [[word i32.ne]]    [bin-0 [binop/ne]]

      [[word i32.lt-s]]  [bin-0 [binop/lt]]
      [[word i32.le-s]]  [bin-0 [binop/le]]
      [[word i32.gt-s]]  [bin-0 [binop/gt]]
      [[word i32.ge-s]]  [bin-0 [binop/ge]]

      [[word i32.and]]   [bin [binop/binary-and]]
      [[word i32.or]]    [bin [binop/binary-ior]]

      [[word f64.add]]   [bin [binop/add]]
      [[word f64.sub]]   [bin [binop/sub]]
      [[word f64.mul]]   [bin [binop/mul]]

      [do
        [log inst-name]
        [logq-abort instruction-name-to-binop not implemented]]]]]

[defn iife [stmt]
  [js-exp/call
    [js-exp/arrow-stmt [list] [none] stmt]
    [list]]]

[defn mk-extern [ws]
  [[func add-sub [o i]
    [if [lt-s i [size ws]]
      [add-sub
        [js-exp/subscript o [js-exp/string [at ws i]]]
        [inc i]]
      o]]
    [js-exp/var [word externs]] [i32 0]]]

[def tmp-counter [atom 0]]

[defn gen-tmp-word []
  [concat-words [word tmp] [int-to-word [inc-atom tmp-counter]]]]

[defn form-to-quoted-js [form]
  [match form
    [form/word w]
    [js-exp/call [js-exp/var [word form/word]] [list [js-exp/string w]]]

    [form/list l]
    [js-exp/call [js-exp/var [word form/list]]
      [list [js-exp/array [list-map e l [form-to-quoted-js e]]]]]]]

[defn compile-top-form [node-to-ldesc outer-is-tail top-form]
  [let
    [current-func [atom [option/none]]
     current-tail-calls [atom [option/none]]
     try-get-tail
     [func tgt [call-form]
        [if-let [ctc [atom-get current-tail-calls]]
          [match ctc
            [tail-call-result/only-tail call-to-deps]
            [try-get call-to-deps call-form]

            [none]]
          [none]]]]
    [letfn
    [[func cfunc [f]
      [let
        [name [bfunc/name f]
         js-name [syntax-word/word name]
         params [bfunc/parameters f]
         js-params [list-map p params [syntax-word/word p]]
         opt-rest-param [bfunc/rest-param f]
         js-opt-rest-param [if-let [rp opt-rest-param] [some [syntax-word/word rp]] [none]]
         body [bfunc/body f]
         tail-analysis [tail-call-analysis node-to-ldesc f]]
        [atom-set current-func [option/some f]]
        [atom-set current-tail-calls [option/some tail-analysis]]
        [let [js-body
              [match tail-analysis
                [tail-call-result/only-tail call-to-deps]
                [do
                  [js-exp/arrow-stmt
                    js-params
                    js-opt-rest-param
                    [block
                      [js-stmt/while js-1
                        [block
                          [cstmt-do true body]
                          [js-stmt/break]]]]]]

                [js-exp/arrow-stmt js-params js-opt-rest-param [cstmt-do true body]]]]
          [iife
            [block
              [js-stmt/const-decl js-name js-body]
              [js-stmt/return [js-exp/var js-name]]]]]]]
     [func cexp [bform]
      [match bform
        [bform/var wf]
        [js-exp/var [syntax-word/word wf]]

        [bform/literal l]
        [match l
          [literal/i32 wf] [js-exp/number [syntax-word/word wf]]
          [literal/f64 wf] [js-exp/number [syntax-word/word wf]]
          [literal/word wf] [js-exp/string [syntax-word/word wf]]]

        [bform/atom init-val]
        [js-exp/call [mk-extern [list [word host] [word atom]]] [list [cexp init-val]]]

        [bform/if c t e]
        [js-exp/ternary [cexp c] [cexp t] [cexp e]]

        [bform/intrinsic i]
        [logq-abort cexp toplevel intrinsic not allowed]

        [bform/extern ws]
        [mk-extern [list-map w ws [syntax-word/word w]]]

        [bform/let lpairs e]
        [iife [cstmt true bform]]

        [bform/letfn fns e]
        [iife
          [js-stmt/block
            [concat
              [list-map fn fns
                [js-stmt/const-decl
                  [syntax-word/word [bfunc/name fn]]
                  [cfunc fn]]]
              [list [cstmt-do true e]]]]]

        [bform/func f]
        [cfunc f]

        [bform/call-fexpr fexpr arg-forms]
        [js-exp/call [cexp fexpr] [list-map arg arg-forms [form-to-quoted-js arg]]]

        [bform/call f args]
        [match f
          [bform/intrinsic i]
          [instruction-name-and-args-to-js-exp [syntax-word/word i] [list-map arg args [cexp arg]]]

          [if-let [deps [try-get-tail bform]]
            [logq-abort cexp found a tail call not expected]
            [js-exp/call [cexp f] [list-map arg args [cexp arg]]]]]

        [bform/type-anno bf t]
        [cexp bf]

        [iife [block [cstmt true bform]]]]]

      [func cstmt-do [is-tail forms]
        [if [is-empty forms]
          [if is-tail
            [js-stmt/return [js-exp/string [word wuns-undefined]]]
            [js-stmt/seq [list]]]
          [js-stmt/block
            [concat
              [list-map f [slice forms 0 [dec [size forms]]] [cstmt 0 f]]
              [list [cstmt is-tail [last forms]]]]]]]

      [func cstmt [is-tail bform]
        [match bform
          [bform/if cond t f]
          [js-stmt/if [cexp cond]
            [block [cstmt is-tail t]]
            [block [cstmt is-tail f]]]

          [bform/switch exp cases default]
          [js-stmt/switch
            [cexp exp]
            [list-map case cases
              [pair
                [list-map lit [pair/fst case] [cexp lit]]
                [if is-tail
                  [cstmt is-tail [pair/snd case]]
                  [block
                    [cstmt is-tail [pair/snd case]]
                    [js-stmt/break]]]]]
            [cstmt is-tail default]]

          [bform/match exp cases opt-default]
          [let [tmp [gen-tmp-word]
                tmp-args [gen-tmp-word]]
            [block
              [js-stmt/const-decl tmp [cexp exp]]
              [js-stmt/const-decl tmp-args [js-exp/subscript [js-exp/var tmp] [js-exp/string [word args]]]]
              [js-stmt/switch
                [js-exp/call
                  [mk-extern [list [word host] [word get-tag]]]
                  [list [js-exp/var tmp]]]
                [list-map case cases
                  [let [mp [pair/fst case]
                        ctor [syntax-word/word [match-pattern/ctor mp]]
                        params [match-pattern/params mp]
                        counter [atom 0]]
                    [pair [list [js-exp/string ctor]]
                      [js-stmt/block
                        [concat
                          [list-map p params
                            [js-stmt/const-decl
                              [syntax-word/word p]
                              [js-exp/subscript [js-exp/var tmp-args] [js-exp/number [int-to-word [inc-atom counter]]]]]]
                          [if is-tail
                            [list [cstmt is-tail [pair/snd case]]]
                            [list
                              [cstmt is-tail [pair/snd case]]
                              [js-stmt/break]]]]]]]]
                [if-let [default-case opt-default]
                  [cstmt is-tail default-case]
                  [js-stmt/throw [js-exp/string [word unmatched-match]]]]]]]

          [bform/do forms]
          [cstmt-do is-tail forms]

          [bform/let decls e]
          [js-stmt/block
            [concat
              [list-map decl decls [js-stmt/const-decl [syntax-word/word [pair/fst decl]] [cexp [pair/snd decl]]]]
              [list [cstmt-do is-tail e]]]]

          [bform/def w e]
          [js-stmt/const-decl [syntax-word/word w] [cexp e]]

          [bform/defexpr fexpr-func]
          [js-stmt/const-decl [syntax-word/word [bfunc/name fexpr-func]] [cfunc fexpr-func]]

          [bform/defmacro macro-func]
          [js-stmt/const-decl [syntax-word/word [bfunc/name macro-func]] [cfunc macro-func]]

          [bform/type decls]
          [js-stmt/seq
            [concat-lists
              [list-map decl decls
                [let [type-name [type-decl/name decl]
                      js-type-name [syntax-word/word type-name]
                      kind [type-decl/kind decl]
                      type-name-prefix [concat-words js-type-name [word /]]]
                  [match kind
                    [type-decl-kind/union ctors]
                    [list-map ctor ctors
                      [let [ctor-name [syntax-word/word [pair/fst ctor]]
                            tag [concat-words type-name-prefix ctor-name]
                            n-atom [atom 0]
                            params [list-map - [pair/snd ctor] [concat-words [word p] [int-to-word [inc-atom n-atom]]]]]
                        [js-stmt/const-decl
                          tag
                          [js-exp/arrow-exp
                            params
                            [none]
                            [js-exp/call
                              [mk-extern [list [word host] [word make-tagged-value]]]
                              [list
                                [js-exp/string tag]
                                [js-exp/array [list-map p params [js-exp/var p]]]]]]]]]

                    [type-decl-kind/record fields]
                    [let [field-names [list-map field fields [syntax-word/word [pair/fst field]]]]
                      [concat
                        [list
                          [js-stmt/const-decl
                            js-type-name
                            [js-exp/arrow-exp
                              field-names
                              [none]
                              [js-exp/call
                                [mk-extern [list [word host] [word make-record-from-object]]]
                                [list
                                  [js-exp/string js-type-name]
                                  [js-exp/object
                                    [list-map field-name field-names
                                      [pair
                                        field-name
                                        [js-exp/var field-name]]]]]]]]]
                        [list-map field-name field-names
                          [js-stmt/const-decl
                            [concat-words type-name-prefix field-name]
                            [js-exp/arrow-exp
                              [list [word record]]
                              [none]
                              [js-exp/subscript
                                [js-exp/var [word record]]
                                [js-exp/string field-name]]]]]]]

                    [logq-abort cstmt unknown type kind]]]]]]

          [bform/call f args]
          [if-let [deps [try-get-tail bform]]
            [let
              [params [if-let [cf [atom-get current-func]] [bfunc/parameters cf] [logq-abort cstmt]]
               deps-set [set]]
              [for-each i deps [set-add deps-set i]]
              [assert is-tail tail call must be tail]

              [let [tmp-consts [growable-list]
                        assignments [growable-list]
                        subst [transient-kv-map]]
                [for i 0 [size args]
                  [let [param [syntax-word/word [at params i]]]
                    [if [set-has deps-set i]
                      [let [tmp [concat-words [word tmp] [int-to-word i]]]
                        [push tmp-consts [js-stmt/const-decl tmp [js-exp/var param]]]
                        [push assignments [js-stmt/assign param [cexp [at args i]]]]
                        [set-kv-map subst param [bform/var [syntax-word tmp [none]]]]]
                      [push assignments [js-stmt/assign param [cexp [substitute [at args i] subst]]]]]]]
                [js-stmt/seq
                  [concat
                    [concat
                      [clone-growable-to-frozen-list tmp-consts]
                      [clone-growable-to-frozen-list assignments]]
                    [list [js-stmt/continue]]]]]]
            [if is-tail
              [js-stmt/return [cexp bform]]
              [js-stmt/exp [cexp bform]]]]

          [if is-tail
            [js-stmt/return [cexp bform]]
            [js-stmt/exp [cexp bform]]]]]]
    [match top-form
      [bform/do forms]
      [js-stmt/seq [list-map f forms [compile-top-form node-to-ldesc outer-is-tail f]]]

      [cstmt outer-is-tail top-form]]]]]

[type compile-run-error []
  [union
    [form-to-ast-errors [list report-message]]
    [eval-error js-value]]]

[defn compile-run-forms [forms]
  [assert [not [is-empty forms]] run-forms must not be empty]
  [let
    [converter [mk-form-to-ast]
     f2a [form-to-ast-converter/form-to-ast converter]
     node-to-ldesc [form-to-ast-converter/node-to-ldesc converter]
     comp-run
     [func comp-run [form]
      [match [f2a form]
        [result/error f2a-errors]
        [do
          [for-each error f2a-errors
            [log-report-message error]]
          [result/error [compile-run-error/form-to-ast-errors f2a-errors]]]

        [result/ok bform]
        [let [stmt [compile-top-form node-to-ldesc true bform]]
          [result/ok stmt]]]]]
    [match [map-result forms comp-run]
      [result/error e] [result/error e]
      [result/ok stmts]
      [match [run-js-stmt [js-stmt/seq stmts] externs-object-value]
        [result/error jsval-err]
        [result/error [compile-run-error/eval-error jsval-err]]

        [result/ok v]
        [result/ok v]]]]]

[defn compile-file-to-file [wuns-file-name js-file-name exports]
  [let
    [converter [mk-form-to-ast]
     f2a [form-to-ast-converter/form-to-ast converter]
     node-to-ldesc [form-to-ast-converter/node-to-ldesc converter]
     comp-run
     [func comp-run [form]
      [match [f2a form]
        [result/error f2a-errors]
        [do
          [for-each error f2a-errors
            [log-report-message error]]
          [result/error [compile-run-error/form-to-ast-errors f2a-errors]]]

        [result/ok bform]
        [let [stmt [compile-top-form node-to-ldesc false bform]]
          [result/ok stmt]]]]]
    [match [map-result [read-file wuns-file-name] comp-run]
      [result/error e]
      [log e]

      [result/ok stmts]
      [let [
          prologue [list [js-stmt/import [word externs] [word ../runtime-lib/externs.js]]]
          epilogue [list [js-stmt/export exports]]]
        [write-js-stmt js-file-name
          [js-stmt/seq
            [concat
              [concat prologue stmts]
              epilogue]]
        ]
      ]]]]
